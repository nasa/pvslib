%trees, defined for directed graphs 
%
%  Authors:
%
%      Ricky W. Butler      NASA Langley Research Center
%      Kristin Y. Rozier    NASA Langley Research Center
%
%  Last updated: 7/28/2004

trees[T: TYPE]: THEORY
BEGIN

   IMPORTING digraph_deg[T], digraph_ops[T], di_subgraphs[T]

   G,GP,C,GG: VAR digraph[T]
   v: VAR T

   leaf?(v,G): MACRO bool = in_deg(v,G) = 1 AND out_deg(v,G) = 0

   leaf?(G)(v): bool = leaf?(v,G)

   root?(v,G): MACRO bool = in_deg(v,G) = 0 AND vert(G)(v)

   root?(G)(v): bool = root?(v,G)

   tree?(G): RECURSIVE bool =
   	          (card[T](vert(G)) = 1 AND card[edgetype[T]](edges(G)) = 0) OR
                  (EXISTS (v: (vert(G))): leaf?(G)(v) AND tree?(del_vert[T](G,v)))
             MEASURE size(G)

   Tree: TYPE = {G: digraph[T] | tree?(G)}

   tree_nonempty: LEMMA tree?(G) IMPLIES NOT empty?(G)

   tree_size_pos: LEMMA tree?(G) IMPLIES size(G)>0
   
   no_self_loops?(G): bool = FORALL (e: (edges(G))): e`1 /= e`2

   tree_no_self_loops: LEMMA tree?(G) IMPLIES no_self_loops?(G)

   root_unique : LEMMA tree?(G) IMPLIES exists1(root?(G))

   root(G : Tree) : T = {v | root?(G)(v)}

   tree_in_deg_max : LEMMA tree?(G) IMPLIES in_deg(v,G) <= 1

END trees
