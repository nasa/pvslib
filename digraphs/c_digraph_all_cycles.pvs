%==============================================================================%
%                                                                              %
% This theory defines an algorithm to get all the cycles of a digraph, given   %
% its computational representation.                                            %
%                                                                              %
%   Mariano Moscato        National Institute of Aerospace (US)   2015         %
%                                                                              %
%==============================================================================%
c_digraph_all_cycles[ T:TYPE]: THEORY
BEGIN
  IMPORTING c_digraphs_def[T],
            orders@ordered_nat,
            structures@more_list_props

  C: VAR (cdigraph)

  loi_cons: LEMMA FORALL(n:nat,l:list[nat]):
    list_of_idxs(C)(cons(n,l)) IMPLIES list_of_idxs(C)(l)

  % index walks.
  walk?(C)(l:(list_of_idxs(C))): bool =
    NOT null?(l) AND
    FORALL(i:below(length(l)-1)): member(nth(l,i+1),nth(adjs(C),nth(l,i)))

  IMPORTING c_cycles[T]
  % equivalence between index walks and vertex  walks.
  walk_eq (C)(w1: Walk(C), w2: (walk?(C))): bool = 
    length(w1) = length(w2) AND
    FORALL(i:below(length(w1))): w1(i) = nth(data(C),nth(w2,i))

  walk_append: LEMMA
    FORALL(l1,l2:(list_of_idxs(C))):
      walk?(C)(l1) AND
      walk?(C)(l2) AND
      (null?(l1) OR null?(l2) OR member(car(l2), nth(adjs(C), last(l1))))
      IMPLIES walk?(C)(append(l1,l2))

  walk_eq_inj_1: LEMMA
   FORALL(wi: (walk?(C)), w1,w2: Walk(C)):
    walk_eq(C)(w1,wi) AND
    walk_eq(C)(w2,wi) 
    IMPLIES w1 = w2
    
  circuit?(C)(w:(walk?(C))): bool =
    car(w) = last(w) AND length(w) > 1

  no_repetitions_append: LEMMA FORALL(l1,l2:list[nat]):
    no_repetitions?(l1) AND
    no_repetitions?(l2) AND 
    (FORALL(n:nat): member(n,l1) IMPLIES NOT member(n,l2))
    IFF no_repetitions?(append(l1,l2))

  no_repetitions_prefix: LEMMA FORALL(l1,l2:list[nat]):
    no_repetitions?(l2) AND
    prefix?(l1,l2)
    IMPLIES no_repetitions?(l1)

  norep_loi_length: LEMMA
    FORALL(l: (list_of_idxs(C))|no_repetitions?(l)):
      length(l) <= size(C)

  cycle?(C)(c: (circuit?(C))): bool =
    no_repetitions?(cdr(c))

  cycle_prefix: LEMMA FORALL(c1,c2: (circuit?(C))):
    cycle?(C)(c1) AND
    cycle?(C)(c2) AND
    prefix?(c1,c2)
    IMPLIES c1 = c2

  cycle_construction_loi: LEMMA
  FORALL (C, (walked: (list_of_idxs[T](C)))):
    list_of_idxs[T](C)(reverse[nat](walked));

  cycle_construction: LEMMA
  FORALL(C)(v:below(size(C)))
  	(walked:(list_of_idxs(C))|NOT null?(walked)):
    walk?(C)(reverse(walked)) AND
    no_repetitions?(reverse(walked)) AND
    last(walked) = v AND
    member(v,nth(adjs(C),car(walked)))
    IMPLIES cycle?(C)(reverse[nat](cons[nat](v, walked)))

  circuit_walk_eq: LEMMA
    FORALL(ci: (circuit?(C)), w: Walk(C)): 
      walk_eq(C)(w,ci) 
      IMPLIES circuit?(C,w)

  cycle_walk_eq: LEMMA
    FORALL(ci: (cycle?(C)), w: Walk(C)): 
      walk_eq(C)(w,ci) 
      IMPLIES cycle?(C,w)

  cycle_construction_size: LEMMA
  FORALL (C, (v: below(size[T](C))),
          (walked: (list_of_idxs(C))
               | walk?(C)(reverse[nat](walked)) AND
                  no_repetitions?[nat](reverse[nat](walked)) AND
                   last[nat](walked) = v)):
    size[T](C) - length[nat](walked) >= 0

  cycle_construction_grow: LEMMA
  FORALL (C: (cdigraph[T]), v: below(size[T](C)),
          (walked: (list_of_idxs(C))
               | walk?(C)(reverse[nat](walked)) AND
                  no_repetitions?[nat](reverse[nat](walked)) AND
                   last[nat](walked) = v),
          (walking: (list_of_idxs(C))
               | suffix?[nat]
                     (walking, nth[list[nat]](C`adjs, car[nat](walked)))),
          w: nat, ws: list[nat]):
    NOT member(w, walked) AND NOT w = v AND walking = cons(w, ws) IMPLIES
     list_of_idxs[T](C)(cons[nat](w, walked)) AND
      walk?(C)(reverse[nat](cons[nat](w, walked))) AND
       no_repetitions?[nat](reverse[nat](cons[nat](w, walked))) AND
        last[nat](cons[nat](w, walked)) = v;

  cycles_from_measure(C)(v:below(size(C)))
  	     ((walked:(list_of_idxs(C))| walk?(C)(reverse(walked)) AND
  	     				 no_repetitions?(reverse(walked)) AND
  					 last(walked) = v),
  	      (walking:(list_of_idxs(C)) |
  	                suffix?(walking,nth(adjs(C),car(walked))))) : ordinal
  = lex2(size(C) - length(walked), length(walking))

  cycles_from_aux_1
  : lemma
    forall (C: (cdigraph[T]), v: below(size[T](C)),
            (walked: (list_of_idxs(C))
               | walk?(C)(reverse[nat](walked)) AND
                  no_repetitions?[nat](reverse[nat](walked)) AND
                   last[nat](walked) = v),
            (walking: (list_of_idxs(C))
               | suffix?[nat]
                     (walking, nth[list[nat]](C`adjs, car[nat](walked)))),
            w: nat, ws: list[nat])
    : walking = cons(w, ws) AND w = v IMPLIES
      list_of_idxs[T](C)(reverse[nat](cons[nat](w, walked))) AND
       walk?(C)(reverse[nat](cons[nat](w, walked))) AND
        circuit?(C)(reverse[nat](cons[nat](w, walked))) AND
         cycle?(C)(reverse[nat](cons[nat](w, walked)));

  cycles_from_aux_2
  : lemma
  FORALL (C: (cdigraph[T]), v: below(size[T](C)),
          (walked: (list_of_idxs(C))
               | walk?(C)(reverse[nat](walked)) AND
                  no_repetitions?[nat](reverse[nat](walked)) AND
                   last[nat](walked) = v),
          (walking: (list_of_idxs(C))
               | suffix?[nat]
                     (walking, nth[list[nat]](C`adjs, car[nat](walked)))),
          w: nat, ws: list[nat]):
    walking = cons(w, ws) AND NOT w = v AND NOT member(w, walked) IMPLIES
     list_of_idxs[T](C)(cons[nat](w, walked)) AND
      walk?(C)(reverse[nat](cons[nat](w, walked))) AND
       no_repetitions?[nat](reverse[nat](cons[nat](w, walked))) AND
        last[nat](cons[nat](w, walked)) = v;

  cycles_from_aux_3
  : lemma
  FORALL (C: (cdigraph[T]), v: below(size[T](C)),
          (walked: (list_of_idxs(C))
               | walk?(C)(reverse[nat](walked)) AND
                  no_repetitions?[nat](reverse[nat](walked)) AND
                   last[nat](walked) = v),
          (walking: (list_of_idxs(C))
               | suffix?[nat]
                     (walking, nth[list[nat]](C`adjs, car[nat](walked)))),
          w: nat, ws: list[nat]):
    walking = cons(w, ws) AND NOT w = v AND NOT member(w, walked) IMPLIES
     list_of_idxs[T](C)(ws) AND
      suffix?[nat](ws, nth[list[nat]](C`adjs, car[nat](walked)));


  % all cycles containing the path cons(v,reverse(walked))
  cycles_from(C)(v:below(size(C)))
  	     ((walked:(list_of_idxs(C))| walk?(C)(reverse(walked)) AND
	     				 no_repetitions?(reverse(walked)) AND
					 last(walked) = v),
  	      (walking:(list_of_idxs(C)) |
	                suffix?(walking,nth(adjs(C),car(walked)))))
  :  RECURSIVE
    { cs: list[(cycle?(C))] |
      FORALL(cycle:(cycle?(C))): member(cycle,cs)
      				 IFF EXISTS(w:below(size(C))):
  				       member(w,walking) AND
  				       prefix?(reverse(cons(w,walked)),cycle) }
  = CASES walking OF
       null: null,
       cons(w,ws): IF w=v THEN
                     % cycle found!
       		     cons( reverse(cons(w,walked)),
		           cycles_from(C)(v)(walked,ws) )
	           ELSIF member(w,walked) THEN
		     % no cycle can be constructed using cons(w,walked), since
		     % no cycle strictly contains another cycle.
		     cycles_from(C)(v)(walked,ws)
		   ELSE
		     % it is necessary to move one step further.
		     append(
		       cycles_from(C)(v)(cons(w,walked),nth(adjs(C), w)),
		       cycles_from(C)(v)(walked,ws))
		   ENDIF
    ENDCASES
  MEASURE cycles_from_measure(C)(v)(walked,walking)

  cycles_for(C)(v:below(size(C))): {cs: list[(cycle?(C))] |
      FORALL(cycle:(cycle?(C))): member(cycle,cs) IFF car(cycle) = v}
  = cycles_from(C)(v)((:v:),nth(adjs(C),v))

  %-----------------------------------------------------------------------------%
  % all_cycles
  %-----------------------------------------------------------------------------%
  % For every vertex index i, all_cycles returns the list with all the cycles
  % starting at i. The result is given as list of list of cycles, where the list
  % containing all cycles for i is in the i-th position of the result.

  all_cycles_rec(C)(n:below(size(C))):
  RECURSIVE {cs: list[list[(cycle?(C))]] | length(cs) = n+1 AND
    FORALL(i: below(n+1)):
      FORALL(cycle:(cycle?(C))): member(cycle,nth(cs,i)) IFF car(cycle) = i}
    = append(IF n=0 THEN null ELSE all_cycles_rec(C)(n-1) ENDIF, (:cycles_for(C)(n):))
  MEASURE n

  all_cycles(C): {cs: list[list[(cycle?(C))]] | length(cs) = size(C) AND
    FORALL(i: below(size(C))):
      FORALL(cycle:(cycle?(C))): member(cycle,nth(cs,i)) IFF car(cycle) = i}
  = IF size(C)=0 THEN null ELSE all_cycles_rec(C)(size(C)-1) ENDIF

  %-----------------------------------------------------------------------------%
  % VertList version
  %-----------------------------------------------------------------------------%
  % conversion from index walk to vertex walk.
  w?2w(C)(cw:(walk?(C))):{w:Walk(C)|walk_eq(C)(w,cw)}=
    (# length := length(cw),
       seq := (LAMBDA(i:below(length(cw))): nth(data(C),nth(cw,i))) #)

  w2w?_rec(C)(w:Walk(C),offset:below(length(w))): RECURSIVE { wi : (walk?(C)) |
	                                    length(wi) + offset = length(w) AND
            FORALL(i:below(length(wi))): w(i+offset) = nth(data(C),nth(wi,i)) } =
    cons( idx(data(C),w(offset)) , 
       	  IF offset = length(w)-1 THEN null ELSE w2w?_rec(C)(w,offset+1) ENDIF )
  MEASURE length(w) - offset 

  w2w?(C)(w:Walk(C)):{wi:(walk?(C))|walk_eq(C)(w,wi)} = w2w?_rec(C)(w,0)
    
  w2w?_circuit: LEMMA
   FORALL(w: Walk(C)):
    circuit?(C, w) IMPLIES circuit?(C)(w2w?(C)(w))

  w2w?_cycle: LEMMA
   FORALL(w: Walk(C)):
    cycle?(C,w) IMPLIES cycle?(C)(w2w?(C)(w))

  % conversion from lists of index cycle to lists of vertex walk.
  lc?2lw (C)
         ( v_idx: below(size(C)),
          cycles: list[(cycle?(C))] | FORALL(i:below(length(cycles))): 
  	                                          nth(nth(cycles,i),0) = v_idx )
  : RECURSIVE {cs: list[Walk(C)] | length(cs) = length(cycles) AND
      FORALL(i:below(length(cs))): walk_eq(C)(nth(cs,i),nth(cycles,i))} =
    CASES cycles OF
      null: null,
      cons(cycle,cycles_): cons( w?2w(C)(cycle) , lc?2lw(C)(v_idx,cycles_) )
    ENDCASES
  MEASURE length(cycles)

  % conversion from all_cycles outputs to lists of VertCycle.
  ac2vl_rec (C)
  	    ( offset: nat,
  	     (cs: list[list[(cycle?(C))]] | length(cs)+offset = size(C) AND
                    FORALL (i:     below(length(cs)))
  		    	   (cycle: (cycle?(C))): 
                         member(cycle,nth(cs,i)) IFF car(cycle) = i+offset) ):
                RECURSIVE {l: list[VertCycles(C)] | length(l) = length(cs) AND
           FORALL(n:below(length(l))): nth[T](data(C), n+offset) = vert(nth(l, n))} =
    CASES cs OF
      null: null,
      cons(cycles, cs_): cons( (# vert := nth(data(C),offset),
      		   	       	  cycles := lc?2lw(C)( offset, cycles ) #), 
      		   	       ac2vl_rec(C)(offset+1,cs_))
    ENDCASES
  MEASURE length(cs) 

  % cac2dac(G, (vl:VertList(G)), (C:cdigraph_eq(G)|data(C)=vl))
  % 	 (cs: list[list[(cycle?(C))]] | length(cs) = size(C) AND
  %   	   FORALL(i: below(size(C))):
  %     	     FORALL(cycle:(cycle?(C))): 
  % 	       member(cycle,nth(cs,i)) IFF car(cycle) = i): VCList(G,vl) =
  %   cac2dac_rec(G,vl,C)(0,all_cycles(C))

  IMPORTING c_digraphs[T]

  all_cycles_vl(C): [# vl: VertList(C), vc: VCList(C, vl) #] =
    (# vl := data(C), vc := ac2vl_rec(C)(0,all_cycles(C)) #)

END c_digraph_all_cycles
