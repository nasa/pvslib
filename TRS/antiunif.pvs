%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%  Verification of Syntactic antiunification  %%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Mariano Miguel Moscato    AMA/NASA LaRC Formal Methods
% Maria Julia Dias Lima     Universidade de Brasilia
% Mauricio Ayala-Rincon     Universidade de Brasilia
% Temur Kutsia              RISC/Johannes Kepler Universitaet Linz
% Thaynara Arielly de Lima  Universidade Federal de Goias
%
%    Last modified 6rd December, 2024
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

antiunif
: theory
begin


  importing first_order_substitution,
            ints@max_finite_set_nat

  Term: TYPE+ = first_order_term

% Anti-unification triples (AUTs) consist of a left- and right-hand
% side terms and a varialble label.  Here specified as AUTs.
  AUT: TYPE = [# lhs, rhs : Term , label : variable #]

  makeAUT(lhs, rhs : Term , label : variable): AUT
  = (# lhs:=lhs, rhs:=rhs, label:=label #)

  vars(eq: AUT): finite_set[variable]
  = vars(eq`lhs) ∪ vars(eq`rhs)

  validAUT?(eq: AUT): bool
  = ¬ member(eq`label, vars(eq))

 size(eq: AUT): nat
  = size(eq`lhs) + size(eq`rhs)

 match_DecF?(eq: AUT): bool
    = LET lhs = eq`lhs, rhs = eq`rhs
      IN  (app?(lhs) ∧ app?(rhs)) ∧ sym(lhs) = sym(rhs)

 match_DecP?(eq: AUT): bool
  = LET lhs = eq`lhs, rhs = eq`rhs
    in pair?(lhs) ∧ pair?(rhs)

 match_Synt?(eq: AUT): bool
  = LET lhs = eq`lhs, rhs = eq`rhs
    IN ¬app?(lhs) ∧ ¬pair?(lhs) ∧ lhs = rhs

  match_Sol?(eq: AUT) : bool
  = NOT (match_DecF?(eq) ∨ match_DecP?(eq) ∨ match_Synt?(eq))

 % Classification of Anti-Unification triples 
  AUT_classification : LEMMA
    FORALL(eq: AUT) :
       (match_DecF?(eq) ∨ match_DecP?(eq) ∨ match_Synt?(eq) ∨ match_Sol?(eq))
    ∧  ( (match_DecF?(eq) ∧ ¬ (match_DecP?(eq) ∨ match_Synt?(eq) ∨ match_Sol?(eq)) ) ∨
         (match_DecP?(eq) ∧ ¬ (match_DecF?(eq) ∨ match_Synt?(eq) ∨ match_Sol?(eq))) ∨
	  (match_Synt?(eq) ∧ ¬ (match_DecF?(eq) ∨ match_DecP?(eq) ∨ match_Sol?(eq))) ∨
	   (match_Sol?(eq) ∧ ¬ (match_DecF?(eq) ∨ match_DecP?(eq) ∨ match_Synt?(eq))) )
	   
  List_AUT : TYPE = list[AUT]

  vars(s: List_AUT): RECURSIVE finite_set[variable]
  = if null?(s) then ∅
    else vars(car(s)) ∪ vars(cdr(s)) endif
    measure s by <<

  vars_in_append_List_AUT :   LEMMA
      FORALL (s1, s2: List_AUT)  : vars(append(s1,s2)) = union(vars(s1), vars(s2))

  subset_vars_AUT_AUTs : LEMMA 
    FORALL (s : List_AUT, eq : AUT) :
       member(eq, s) IMPLIES subset?(vars(eq), vars(s))

  labels(s: List_AUT): RECURSIVE finite_set[variable]
  = if null?(s) then ∅
    else add(car(s)`label, labels(cdr(s))) endif
    measure s by <<

  AUT_member_AUT_label_in_labels : LEMMA  
  FORALL(le:List_AUT, eq:AUT) : member(eq, le) IMPLIES member(eq`label, labels(le))

  AUTs_label_AUT_this_label : LEMMA
  FORALL(le:List_AUT, lbl:variable) :  member(lbl, labels(le)) IMPLIES
        EXISTS (eq:AUT) : member(eq, le) AND eq`label = lbl 
  
  card_lbls_in_List_AUT : LEMMA FORALL(le:List_AUT) :  card(labels(le)) <= length(le)

  append_labels_is_union_labels : LEMMA
  FORALL (auts1, auts2 : List_AUT) : labels(append(auts1, auts2)) = union(labels(auts1), labels(auts2))

  valid_AUTs?(s: List_AUT): bool
    = empty?(vars(s) ∩ labels(s)) ∧ card(labels(s)) = length(s)

disjoint_valid_append_valid_AUTs : LEMMA
FORALL (auts1, auts2 : List_AUT) : valid_AUTs?(append(auts1,auts2)) IMPLIES disjoint?(labels(auts1), labels(auts2))

non_member_label_valid_AUTs :  LEMMA
FORALL (auts: (valid_AUTs?) | cons?(auts))  : NOT member(car(auts)`label, labels(cdr(auts)))

validity_cdr_valid_AUTs  : LEMMA  
FORALL (auts: (valid_AUTs?) | cons?(auts)) : valid_AUTs?(cdr(auts))

validity_AUT_in_valid_AUTs  : LEMMA   
FORALL (auts: (valid_AUTs?), i : below[length(auts)]) : validAUT?(nth(auts,i))

non_member_var_nth_label: LEMMA
FORALL (auts: (valid_AUTs?), i: below[length(auts)], v: variable): NOT member(v, labels(auts)) IMPLIES v /= label(nth(auts,i)) 

validity_append_valid_AUTs: LEMMA
 FORALL (l1,l2: List_AUT): valid_AUTs?(append(l1,l2)) IMPLIES valid_AUTs?(l1) AND valid_AUTs?(l2)

  size(auts: List_AUT): nat
  = sum(map(size)(auts))

% Definition of configuration

  Configuration: TYPE = [# unsolved, solved: List_AUT, substitution: (nice?) #]

  size(c: Configuration): nat
  = size(c`unsolved) + size(c`solved)
  
  match_DecF?(s: List_AUT): bool
    = cons?(s) ∧  match_DecF?(car(s))

  match_DecP?(s: List_AUT): bool
  = cons?(s) ∧ match_DecP?(car(s))

  match_Synt?(s: List_AUT): bool
  = cons?(s) ∧ match_Synt?(car(s))
  
  match_Sol?(s: List_AUT) : bool
  = cons?(s) ∧ match_Sol?(car(s))

% Definition of repeated AUTs 
  repeated_AUT?(eq1,eq2 :  AUT) : bool =
    eq1`lhs = eq2`lhs ∧ eq1`rhs = eq2`rhs
    
  AUT_repeated_in?(eq: AUT, s: List_AUT) :  RECURSIVE bool
   =  cons?(s) ∧
    ( repeated_AUT?(eq,car(s)) ∨ AUT_repeated_in?(eq, cdr(s)) )
  MEASURE s by <<

  first_AUT_repeated?(s:List_AUT) :  bool
    = cons?(s) ∧ AUT_repeated_in?(car(s),cdr(s))

  cdr_first_AUT_repeated: LEMMA
  FORALL (s: (first_AUT_repeated?)): cons?(cdr(s))

% Selection of the second occurrence of a head repeated equation in a list
  rep_AUT_in(s: (first_AUT_repeated?)) : RECURSIVE AUT =
    IF repeated_AUT?(car(s), car(cdr(s))) 
    THEN car(cdr(s))
    ELSE rep_AUT_in(cons(car(s), cdr(cdr(s))))
    ENDIF
    MEASURE length(s)

  repeated_AUT_correct : LEMMA
    FORALL (s: (first_AUT_repeated?)) :
      rep_AUT_in(s)`lhs = car(s)`lhs AND rep_AUT_in(s)`rhs = car(s)`rhs

  rep_AUT_in_cdr : LEMMA
     FORALL (s: (first_AUT_repeated?)) : member(rep_AUT_in(s),cdr(s))

  nonrepeated?(s: List_AUT) : RECURSIVE bool  =
    length(s) <= 1 ∨ 
    ( NOT first_AUT_repeated?(s) ∧ nonrepeated?(cdr(s)))
    MEASURE s by << 

  allSolvedAUTs?(s: List_AUT)  : RECURSIVE bool =
    null?(s) ∨ (match_Sol?(car(s)) ∧ allSolvedAUTs?(cdr(s)))
    MEASURE s by <<

% List of valid solved equations:  all equations are solved and not repeated.
  validSolvedAUTs?(s: List_AUT) : bool =
   allSolvedAUTs?(s) ∧ nonrepeated?(s)

% Definition of valid configuration
  validConfiguration?(c: Configuration): bool
  = LET allAUTs = append(c`unsolved, c`solved)
    in valid_AUTs?(allAUTs) ∧ empty?(supset_dom(c`substitution) ∩ labels(allAUTs)) ∧ 
        validSolvedAUTs?(c`solved) ∧ empty?(supset_dom(c`substitution) ∩ vars(allAUTs))

  disjoint_labels_unsolved_solved : COROLLARY
    FORALL (c: (validConfiguration?)) : disjoint?(labels(c`unsolved), labels(c`solved))

  validity_car_conf_unsolved  : COROLLARY
   FORALL (c: (validConfiguration?)| cons?(c`unsolved)) : validAUT?(car(c`unsolved))

  validity_cdr_conf_unsolved  : COROLLARY 
    FORALL (c: (validConfiguration?)| cons?(c`unsolved), eq: AUT ) : member(eq, cdr(c`unsolved)) IMPLIES
   	      			     			                   validAUT?(eq)

% Set of variables (labels and variables) in a configuration  
  vars(c :(validConfiguration?)) : finite_set[variable] =
    LET allAUTs = append(c`unsolved, c`solved)  IN
      union(union(vars(allAUTs), labels(allAUTs)), 
          union(supset_dom(c`substitution), vars(img(c`substitution))))

% No label belongs to the domain of the substitution in a valid configuration.
  invariance_labels_in_validConf  : LEMMA
     FORALL (c : (validConfiguration?))  :
        LET allAUTs = append(c`unsolved, c`solved) IN
             FORALL (l : (labels(allAUTs)) ):
	       subs(c`substitution)(variable(l)) = variable(l)

  cdr_is_validConf  :  LEMMA
     FORALL (c : (validConfiguration?) | cons?(c`unsolved) ) :
        validConfiguration?(c with [unsolved := cdr(c`unsolved)])

 labels_allAUTs_as_union: LEMMA
    FORALL (c: (validConfiguration?)):
     labels(append(c`unsolved, c`solved)) = union(labels(c`unsolved),labels(c`solved))

  freshLabel(V : setof[variable]) : variable =
    epsilon((lambda(v:variable): NOT member(v,V))) 

% Construction of fresh variables using the PVS epsilon function
  freshLabel(c:(validConfiguration?)): variable =
    freshLabel(vars(c))

  freshLabel(c:(validConfiguration?),V : finite_set[variable]): variable =
    freshLabel(union(vars(c),V)) 
  
  freshness_membship : LEMMA
      FORALL (V : finite_set[variable]) : NOT member(freshLabel(V),V)

  freshness_epsilon_ext : COROLLARY
      FORALL (c:(validConfiguration?),V : finite_set[variable]) : NOT member(freshLabel(c,V), union(vars(c),V))

  freshness_epsilon : COROLLARY FORALL (c:(validConfiguration?)) : NOT member(freshLabel(c),vars(c))

  freshness_vars :  COROLLARY FORALL (c:(validConfiguration?)) :
               NOT member(freshLabel(c), vars(append(c`unsolved,c`solved)))

  freshness_labels :  COROLLARY FORALL (c:(validConfiguration?)) :
               NOT member(freshLabel(c), labels(append(c`unsolved,c`solved)))

 freshness_nth_label : LEMMA FORALL (c:(validConfiguration?), i : below[length(c`unsolved) + length(c`solved)]) :
       NOT freshLabel(c) = (nth(append(c`unsolved, c`solved),i))`label

  freshness_car_label : COROLLARY FORALL (c:(validConfiguration?)| cons?(c`unsolved)) :
       NOT freshLabel(c) = label(car(c`unsolved))

  freshness_labels_ext :  COROLLARY FORALL (c:(validConfiguration?),V : finite_set[variable]) :
               NOT member(freshLabel(c,V), labels(append(c`unsolved,c`solved)))
 
  freshness_subs : COROLLARY FORALL (c:(validConfiguration?)) :
               NOT member(freshLabel(c), union(supset_dom(c`substitution), vars(img(c`substitution))))

  freshness_subs_ext : COROLLARY FORALL (c:(validConfiguration?), V : finite_set[variable]) :
               NOT member(freshLabel(c,V), union(supset_dom(c`substitution), vars(img(c`substitution))))

  freshness_subs_dom_ext: COROLLARY FORALL (c:(validConfiguration?), V : finite_set[variable]) :
               NOT member(freshLabel(c,V), dom(c`substitution))

  car_lbl_fresh_in_cdr :  LEMMA FORALL (c:(validConfiguration?) | cons?(c`unsolved) ) :
               NOT member(label(car(c`unsolved)), labels(append(cdr(c`unsolved),c`solved)))

  car_lbl_fresh_dom: LEMMA FORALL (c:(validConfiguration?) | cons?(c`unsolved) ) :
               NOT member(label(car(c`unsolved)), dom(c`substitution))

  emptyness_conf_vars_with_lbls_and_fresh_variables: LEMMA FORALL (c:(validConfiguration?) | cons?(c`unsolved), set_lbl : finite_set[variable]):
  	     (FORALL (lbl:variable) : member(lbl,set_lbl) IMPLIES NOT member(lbl,vars(c))) IMPLIES
	      empty?(intersection(vars(append(c`unsolved, c`solved)),
	             union(set_lbl, labels(append(cdr(c`unsolved), c`solved)))))

  emptyness_conf_supdom_with_lbls_and_fresh_variables: LEMMA
    FORALL (c:(validConfiguration?) | cons?(c`unsolved), set_lbl : finite_set[variable]):
    (FORALL (lbl:variable) : member(lbl,set_lbl) IMPLIES NOT member(lbl,vars(c))) IMPLIES
     empty?(intersection(supset_dom(c`substitution), union(set_lbl,labels(append(cdr(c`unsolved),c`solved)))))

  emptyness_conf_supdom_with_car_lbls_and_fresh_variables: LEMMA
    FORALL (c:(validConfiguration?) | cons?(c`unsolved), set_lbl : finite_set[variable]):
    LET eq = car(c`unsolved) IN
    (FORALL (lbl:variable) : member(lbl,set_lbl) IMPLIES NOT member(lbl,vars(c))) IMPLIES
     empty?(intersection(union(singleton(eq`label), supset_dom(c`substitution)), union(set_lbl,labels(append(cdr(c`unsolved),c`solved)))))

  emptyness_conf_var_with_lbls_DecF: COROLLARY FORALL (c:(validConfiguration?) | cons?(c`unsolved), lbl:variable):
  	     NOT member(lbl,vars(c)) IMPLIES
	     empty?(intersection(vars(append(c`unsolved, c`solved)), add(lbl, labels(append(cdr(c`unsolved), c`solved)))))

  emptyness_conf_var_with_lbls_DecP: COROLLARY FORALL (c:(validConfiguration?) | cons?(c`unsolved)):
  	     LET lbl1 = freshLabel(c) IN
	     LET lbl2 = freshLabel(union(vars(c), singleton(lbl1))) IN
	      empty?(intersection(vars(append(c`unsolved, c`solved)), add(lbl1, add(lbl2,
                       labels(append(cdr(c`unsolved), c`solved))))))

 emptyness_conf_var: COROLLARY FORALL (c:(validConfiguration?) | cons?(c`unsolved)):
	     empty?(intersection(vars(append(c`unsolved, c`solved)), (labels(append(cdr(c`unsolved), c`solved)))))

  niceness_preserv_conditions: LEMMA
  FORALL (c:(validConfiguration?) | cons?(c`unsolved), set_lbl : finite_set[variable]):
    LET eq = car(c`unsolved) IN
    (FORALL (lbl:variable) : member(lbl,set_lbl) IMPLIES NOT member(lbl,vars(c))) IMPLIES
     (NOT member(eq`label, union(set_lbl, supset_dom(c`substitution))) AND
      empty?(intersection(set_lbl,
                          union(supset_dom(c`substitution), vars(img(c`substitution))))))

    
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%*******%%%%
%%%%%%%%%%% Decompose-Function (DecF) inference rule %%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
  match_DecF_conf?(c: (validConfiguration?)): bool = match_DecF?(c`unsolved)

  niceness_preserv_conditions_DecF: COROLLARY FORALL (c: (match_DecF_conf?)):
    LET eq = car(c`unsolved) IN
    LET lbl = freshLabel(c) IN
     NOT member(eq`label, union(singleton(lbl), supset_dom(c`substitution))) AND
     empty?(intersection(singleton(lbl),
                         union(supset_dom(c`substitution), vars(img(c`substitution)))))

  nice_sub_DecF: COROLLARY
     FORALL (c: (match_DecF_conf?)):
     LET eq = car(c`unsolved),
         lbl = freshLabel(c),
	 sigma = c`substitution IN
     nice?(cons((eq`label, app(sym(eq`lhs),variable(lbl))),sigma));

  uns_solv_vars_match_DecF_conf: LEMMA FORALL (c: (match_DecF_conf?)):
         LET eq = car(c`unsolved),
             lhs = eq`lhs,
             rhs = eq`rhs IN
         vars(append(c`unsolved, c`solved)) = union(vars(arg(lhs)), union(vars(arg(rhs)),
	 			 	       vars(append(cdr(c`unsolved), c`solved))))

  DecF(c: (match_DecF_conf?)): {cp : (validConfiguration?) |  cons?(cp`unsolved) AND
                                            cdr(c`unsolved) = cdr(cp`unsolved) AND
                                            subs(cp`substitution)(variable(car(c`unsolved)`label)) =
	                         	        app(sym((car(c`unsolved))`lhs),
						variable(car(cp`unsolved)`label)) AND
						size(cp`unsolved) < size(c`unsolved)}                                                        
   = LET eq = car(c`unsolved),
         lhs = eq`lhs,
         rhs = eq`rhs,
         lbl = freshLabel(c)
    IN c with [ unsolved := cons(  makeAUT(arg(lhs),arg(rhs),lbl), cdr(c`unsolved)),
                substitution := cons(  (eq`label,  app(sym(eq`lhs),variable(lbl))), c`substitution) ]


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%% Decompose-Pairs (DecP) inference rule %%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  match_DecP_conf?(c: (validConfiguration?)): bool = match_DecP?(c`unsolved)

  niceness_preserv_conditions_DecP: LEMMA FORALL (c: (match_DecP_conf?)):
     LET eq = car(c`unsolved), 
     	 lbl1 = freshLabel(c),
	 lbl2 = freshLabel(union(vars(c),singleton(lbl1))),
	 set_lbl = {x : variable | x = lbl1 OR x = lbl2} IN
     NOT member(eq`label, union(set_lbl, supset_dom(c`substitution))) AND
      empty?(intersection(set_lbl,
                          union(supset_dom(c`substitution), vars(img(c`substitution)))))

   nice_sub_DecP: LEMMA
       FORALL (c: (match_DecP_conf?)):
       LET eq = car(c`unsolved),
           lbl1 = freshLabel(c),
   	  lbl2 = freshLabel(union(vars(c),singleton(lbl1))),
   	  sigma = c`substitution IN
	nice?(cons((eq`label,pair(variable(lbl1),variable(lbl2))),
                    sigma))
		     
  uns_solv_vars_match_DecP_conf: LEMMA FORALL (c: (match_DecP_conf?)):
          LET eq = car(c`unsolved),
              lhs = eq`lhs,
              rhs = eq`rhs IN
          vars(append(c`unsolved, c`solved)) = union(vars(term1(lhs)), union(vars(term1(rhs)),
	  			  	       union(vars(term2(lhs)), union(vars(term2(rhs)),
  	 			 	       vars(append(cdr(c`unsolved), c`solved))))))
					       

  DecP(c: (match_DecP_conf?)): {cp : (validConfiguration?) | cons?(cp`unsolved) AND
                                            cons?(cdr(cp`unsolved)) AND cdr(cdr(cp`unsolved)) = cdr(c`unsolved) AND
                                            subs(cp`substitution)(variable(car(c`unsolved)`label)) =
					       pair(variable(car(cp`unsolved)`label),
					       variable(car(cdr(cp`unsolved))`label))  AND
				            size(cp`unsolved) < size(c`unsolved)}
  = LET eq = car(c`unsolved), lhs = eq`lhs, rhs = eq`rhs,
	lbl1 = freshLabel(c),
	lbl2 = freshLabel(union(vars(c),singleton(lbl1))) 
    IN c with [ unsolved := cons( makeAUT(term1(lhs),term1(rhs),lbl1),
                                  cons(makeAUT(term2(lhs),term2(rhs),lbl2), cdr(c`unsolved))),
                substitution := cons( (eq`label, pair(variable(lbl1),variable(lbl2))), c`substitution) ]


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%% Syntactic inference rule  %%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  match_Synt_conf?(c: (validConfiguration?)): bool
  = match_Synt?(c`unsolved)

  nice_sub_Synt: LEMMA FORALL (c: (match_Synt_conf?)):   
    LET eq = car(c`unsolved) IN
    nice?(cons((eq`label, eq`lhs), c`substitution))

  Synt(c: (match_Synt_conf?)): {cp : (validConfiguration?) |
  		    			       subs(cp`substitution)(variable(car(c`unsolved)`label)) =
					        car(c`unsolved)`lhs AND 
					       size(cp`unsolved) < size(c`unsolved)}
  =   LET eq = car(c`unsolved), lhs = eq`lhs
      IN  c with [unsolved := cdr(c`unsolved),
                 substitution := cons( (eq`label, lhs),  c`substitution ) ]

 syntAUT_inter_vars_unsolv_labels_solv: LEMMA
    FORALL (c: (match_Synt_conf?)):
      empty?(intersection(vars(car(c`unsolved)), labels(Synt(c)`solved)))

 labels_Synt: LEMMA
   FORALL (c: (match_Synt_conf?)):
       labels(Synt(c)`solved) = labels(c`solved)

 syntAUT_car_lhs_member_img: LEMMA
   FORALL (c: (match_Synt_conf?)):
      member(car(c`unsolved)`lhs, img(Synt(c)`substitution))

 domain_Synt: LEMMA
   FORALL (c: (match_Synt_conf?)):
   LET lbl = car(c`unsolved)`label IN
   dom(Synt(c)`substitution) = union(singleton(lbl), dom(c`substitution))

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%% Solve inference rule  %%%%%%%%%%%%%%%%%%%%%%
%%%% It integrates both Solve-Repeated and Solve-Non-Repeated %%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

  match_Sol_conf?(c: (validConfiguration?)): bool
  = match_Sol?(c`unsolved)

 nice_sub_Solved_AUT: LEMMA FORALL (c: (match_Sol_conf?)):   
     AUT_repeated_in?(car(c`unsolved), c`solved) IMPLIES
       LET sol_AUT = car(c`unsolved),  rep_AUT = rep_AUT_in(cons(sol_AUT,c`solved))  IN
       nice?(cons((sol_AUT`label, variable(rep_AUT`label)), c`substitution))
	 
  Solve(c: (match_Sol_conf?)): {cp : (validConfiguration?) | size(cp`unsolved) < size(c`unsolved)}
  = IF AUT_repeated_in?(car(c`unsolved), c`solved) THEN
       LET sol_AUT =car(c`unsolved), rep_AUT = rep_AUT_in(cons(sol_AUT,c`solved)) IN
         c with [unsolved := cdr(c`unsolved),
	         substitution:= cons( (sol_AUT`label, variable(rep_AUT`label)), c`substitution)] 
    ELSE c with [unsolved := cdr(c`unsolved),
                 solved := cons(car(c`unsolved),c`solved)]
    ENDIF	    


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%% Antiunification algorithm %%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

 antiunify(c: (validConfiguration?)): recursive (validConfiguration?)
  =  IF cons?(c`unsolved)
     THEN
       IF match_DecF_conf?(c)
       THEN      % Apply "Decompose-Function" rule
            antiunify(DecF(c))
       ELSIF match_DecP_conf?(c)
       THEN      % Apply "Decompose-Pairs" rule
            antiunify(DecP(c))
       ELSIF match_Synt_conf?(c)
       THEN      % Apply "Syntactic" rule 
            antiunify(Synt(c))
       ELSE      % Apply "Solve" rule
            antiunify(Solve(c)) 
       ENDIF
      ELSE c
      ENDIF
  MEASURE size(c`unsolved)

% Definition of substitution generalizer of an AUT 
 generalizer?(eq: (validAUT?), sigma, rho, delta: (nice?)) : bool =
   subs(append(rho,sigma))(eq`label) = eq`lhs AND
   subs(append(delta,sigma))(eq`label) = eq`rhs
   
% Definition of substitution generalizer of a list of AUTs 
 generalizer?(auts : (valid_AUTs?), sigma, rho, delta: (nice?)) : bool =
  FORALL (eq : (validAUT?)) : member(eq, auts) IMPLIES
        generalizer?(eq, sigma, rho, delta)
	
% Construction of the left-hand side substitution from a list of AUTs
build_subs_left(auts : (valid_AUTs?)) : RECURSIVE sub =
  IF null?(auts) THEN null
  ELSE LET aut = car(auts) IN
       cons((aut`label, aut`lhs), build_subs_left(cdr(auts)))
  ENDIF
MEASURE length(auts)

super_domain_subs_left: LEMMA
           FORALL (auts : (valid_AUTs?)) :  supset_dom(build_subs_left(auts)) = labels(auts)

domain_subs_left: COROLLARY
           FORALL (auts : (valid_AUTs?)): subset?(dom(build_subs_left(auts)), labels(auts))

nice_subs_left: LEMMA
FORALL (auts : (valid_AUTs?)) : nice?(build_subs_left(auts))

% Construction of the right-hand side substitution from a list of AUTs
build_subs_right(auts :  (valid_AUTs?)) : RECURSIVE sub =
  IF null?(auts) THEN null
  ELSE LET aut = car(auts) IN
       cons((aut`label, aut`rhs), build_subs_right(cdr(auts)))
  ENDIF
MEASURE length(auts)

super_domain_subs_right: LEMMA
           FORALL (auts : (valid_AUTs?)) :  supset_dom(build_subs_right(auts)) = labels(auts)

domain_subs_right: COROLLARY FORALL (auts : (valid_AUTs?)):    
   subset?(dom(build_subs_right(auts)), labels(auts))

nice_subs_right: LEMMA
FORALL (auts : (valid_AUTs?)) : nice?(build_subs_right(auts))

images_of_build_subs_left_right : LEMMA FORALL(auts : (valid_AUTs?), eq: AUT) :
    member(eq,auts) IMPLIES
              LET rho_l = build_subs_left(auts),
	          rho_r = build_subs_right(auts) IN
		           subs(rho_l)(eq`label) = eq`lhs AND 
                           subs(rho_r)(eq`label) = eq`rhs 

% Configuration characterization: normal, decomposable, solvable, and syntactic decomposable
 normal_configuration?(c : (validConfiguration?)) : bool = null?(c`unsolved)

 antiunify_normality :  LEMMA
   FORALL(c:(normal_configuration?)) : antiunify(c) = c

 antiunify_derivability :  LEMMA 
    FORALL(c:(validConfiguration?)) : NOT normal_configuration?(c) =>
      match_DecF_conf?(c) OR  match_DecP_conf?(c)  OR
      match_Synt_conf?(c) OR match_Sol_conf?(c)

 match_DecP_classification: LEMMA
   FORALL(c:(validConfiguration?)) : NOT normal_configuration?(c) IMPLIES
    (match_DecP_conf?(c) IMPLIES NOT match_DecF_conf?(c))

 match_Synt_classification: LEMMA
   FORALL(c:(validConfiguration?)) : NOT normal_configuration?(c) IMPLIES
   (match_Synt_conf?(c) IMPLIES NOT (match_DecF_conf?(c) OR match_DecP_conf?(c)))

 SolvedAUT_classification: LEMMA
   FORALL(c:(validConfiguration?)) : NOT normal_configuration?(c) IMPLIES
   (match_Sol_conf?(c) IMPLIES NOT (match_DecF_conf?(c) OR match_DecP_conf?(c) OR match_Synt_conf?(c)))

%%%%%
%%% Configuration invariants and preservation constraints
%%%%

% Preservation of solved AUTs into a configuration
 antiunify_monotony_Sol_AUTs : LEMMA 
   FORALL (c : (validConfiguration?), eq : AUT | member(eq, c`solved)) :
       member(eq, antiunify(c)`solved) 

% Preservation of the solved labels outside the domain of the final
% substitution generalization
 antiunify_domain_disjoint_sol_labels : LEMMA 
    FORALL (c : (validConfiguration?)) : disjoint?(dom(antiunify(c)`substitution), labels(c`solved))

% Construction incremental of the final substitution generalization
 antiunify_sub_decomposition: LEMMA
  FORALL(c:(validConfiguration?)) : EXISTS (theta:(nice?)) : antiunify(c)`substitution = append(theta, c`substitution)

% Preservation of terms without variables proceeding from labels (i.e., terms of the problem)
 antiunify_sub_preserves_terms: LEMMA
  FORALL(c:(validConfiguration?), t: Term): (member(t, img(c`substitution)) AND
  				     	    empty?(intersection(vars(t), labels(c`unsolved)))) IMPLIES
					    subs(antiunify(c)`substitution)(t) = subs(c`substitution)(t)

% Variables in terms of the unsolved part do not move to the domain of the computed generalizer
 antiunify_dom_sub_preserves_vars_unsolved: LEMMA   
  FORALL(c:(validConfiguration?)) : intersection(dom(antiunify(c)`substitution), vars(c`unsolved)) = emptyset

% Another version of preservation of terms without variables proceeding from labels; this one
% regarding labels in the solved part of the final configuration. 
 antiunify_lbls_preserves_vars_unsolved: LEMMA
  FORALL(c:(validConfiguration?), t: Term): member(t, img(c`substitution)) AND
  				     	    disjoint?(vars(t), labels(append(c`unsolved, c`solved))) IMPLIES
					    disjoint?(vars(t), labels(antiunify(c)`solved))

% Variables in terms of the unsolved part and labels in the final solved part are disjoint
 antiunify_solved_labels_preserve_vars_unsolved: LEMMA   
  FORALL(c:(validConfiguration?)) : intersection(labels(antiunify(c)`solved), vars(c`unsolved)) = emptyset

% Auxiliary lemma on the construction of left- and right-hand side final substitutions. 
 antiunify_solved_substitution:  LEMMA
  FORALL(c:(validConfiguration?), eq:AUT) :
    member(eq,c`solved) IMPLIES
              LET rho_l = build_subs_left(antiunify(c)`solved),
	          rho_r = build_subs_right(antiunify(c)`solved) IN
		           subs(rho_l)(eq`label) = eq`lhs AND 
                           subs(rho_r)(eq`label) = eq`rhs 

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%% Antiunification - Soundness %%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

antiunify_is_sound  : THEOREM
  FORALL (ci:(validConfiguration?)) :
     LET cf = antiunify(ci),
              sigma = cf`substitution,
	      rho_l = build_subs_left(cf`solved),
	      rho_r = build_subs_right(cf`solved) IN
      generalizer?(ci`unsolved, sigma, rho_l, rho_r)

end antiunif

