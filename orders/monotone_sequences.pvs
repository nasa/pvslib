% infinite ascending and descending sequences and their subsequences
%
% Author: Alfons Geser (geser@nianet.org), National Institute of Aerospace
% Date: Dec 2004

monotone_sequences[T: TYPE]: THEORY

BEGIN

  IMPORTING closure_ops[T]

  k,n: VAR nat
  x, y: VAR T
  rel: VAR pred[[T, T]]
  seq: VAR sequence[T]
  lt: VAR (transitive?[T])
  asc: VAR (preserves[nat, nat](<, <))

  % these definitions do not require rel to be a partial order

  ascending?(rel)(seq): bool =
    FORALL n: rel(seq(n), seq(n + 1))

  descending?(rel)(seq): bool =
    FORALL n: rel(seq(n + 1), seq(n))

  reflexive_closure_ascending: LEMMA
    FORALL (seq: (ascending?(rel))): ascending?(reflexive_closure(rel))(seq)

  reflexive_closure_descending: LEMMA
    FORALL (seq: (descending?(rel))): descending?(reflexive_closure(rel))(seq)

  ascending_lem: LEMMA
    FORALL (seq: (ascending?(lt))): preserves(seq, <, lt)

  descending_lem: LEMMA
    FORALL (seq: (descending?(lt))): preserves(seq, <, converse(lt))

  descending_converse_eq_ascending : LEMMA
    descending?(converse(rel)) = ascending?(rel)

  ascending_converse_eq_descending : LEMMA
    ascending?(converse(rel)) = descending?(rel)

  ascending_suffix : LEMMA
    FORALL (seq: (ascending?(rel)))(n:nat) :
      ascending?(rel)(suffix(seq,n))

  % Non strict sequences are those that allow for finite reflexivity (unless rel is itself reflexive)
  ascending_non_strict?(rel)(seq) : bool =
    (FORALL n: rel(seq(n), seq(n + 1)) OR seq(n)=seq(n+1)) AND
    (FORALL n: EXISTS (k:upfrom(n)): rel(seq(k),seq(k+1))) 

  strict_from_rec(rel)(seq:(ascending_non_strict?(rel)))(n:nat) : RECURSIVE
    { (k,x) |  x=seq(k) AND
               (k > 0 IMPLIES rel(seq(k-1),seq(k))) } =
    IF n = 0 THEN (0,seq(0))
    ELSE
      LET  (nk,nx) = strict_from_rec(rel)(seq)(n-1),
           minj = min({j:above(nk) | rel(seq(j-1),seq(j))}) IN
      (minj,seq(minj))
    ENDIF
  MEASURE n

  strict_from_prop : LEMMA
    FORALL (rel)(seq:(ascending_non_strict?(rel)))(n:nat) :
      LET (k1,x1) = strict_from_rec(rel)(seq)(n),
          (k2,x2) = strict_from_rec(rel)(seq)(n+1) IN
        k1 < k2 AND
	rel(seq(k1),seq(k2)) AND
	FORALL (i:subrange(k1,k2-1)): seq(i)=seq(k1)

  strict_from(rel)(seq:(ascending_non_strict?(rel))) : (ascending?(rel)) =
    LAMBDA(i:nat):strict_from_rec(rel)(seq)(i)`2

  % subsequences
  ascending_subsequence: THEOREM
    FORALL (seq: (ascending?(lt))): ascending?(lt)(seq o asc)

  descending_subsequence: THEOREM
    FORALL (seq: (descending?(lt))): descending?(lt)(seq o asc)

  %1/27/21 JTS, 1/29/21 CM: fseq goes up to n
  ascending_n?(n)(rel)(fseq:ARRAY[upto(n)->T]): bool =
    FORALL (i:below(n)): rel(fseq(i),fseq(i+1))

  ascending_iterate_n : LEMMA
    FORALL(fseq:(ascending_n?(n)(rel)))(i:below(n),j:subrange(i,n)):
      iterate_n(j-i)(rel)(fseq(i),fseq(j))

  iterate_n_fseq: LEMMA
    FORALL(x,y:T,rel,n):
      iterate_n(n)(rel)(x,y) IFF
      EXISTS (fseq:(ascending_n?(n)(rel))) : fseq(0) = x AND fseq(n) =y

END monotone_sequences
