% Reduction of cubic solution to quadratic solution using Vieta's method

vieta : THEORY
BEGIN

  IMPORTING reals@power_props,
	    reals@sign3,
	    complex_types,
	    complex_root,
	    complex_quadratic,
	    fundamental_algebra,
	    complex_props

  a         : VAR nzreal
  b,c,d,p,q : VAR real

%wikipedia

  cubic_discr(a,b,c,d):real =
    18*a*b*c*d-4*b^3*d+b^2*c^2-4*a*c^3-27*a^2*d^2
    
  delta0(a,b,c):real =
    sq(b)-3*a*c

  delta1(a,b,c,d):real =
    2*b^3-9*a*b*c+27*sq(a)*d

  delta_expr(a,b,c,d): MACRO real =
    sq(delta1(a,b,c,d))-4*delta0(a,b,c)^3

  delta_expr_cubic_discr: LEMMA
    delta_expr(a,b,c,d) = -27*a^2*cubic_discr(a,b,c,d)

  delta_expr_eq_0 : LEMMA
    delta_expr(a,b,c,d) = 0 IFF cubic_discr(a,b,c,d) = 0 

  cubic_discr_eq_0 : LEMMA
    cubic_discr(a,b,c,d) = 0 IMPLIES
    (delta0(a,b,c) = 0 IFF delta1(a,b,c,d) = 0)

  P(a,b,c) : real = -delta0(a,b,c)/(3*sq(a))

  P_eq_0 : LEMMA
    P(a,b,c) = 0 IFF delta0(a,b,c) = 0

  Q(a,b,c,d) : real = delta1(a,b,c,d)/((3*a)^3)

  Q_eq_0 : LEMMA
    Q(a,b,c,d) = 0 IFF delta1(a,b,c,d) = 0

  discr_cubic_discr : LEMMA
    discr(1,Q(a, b, c, d),-P(a, b, c)^3/27) = -cubic_discr(a,b,c,d)/(27*a^4)

  c_cubic(a,b,c,d)(x:complex) : complex =
    a*x^3 + b*x^2 + c*x + d

  IMPORTING cpolynomial_real

  cpoly_cubic(a,b,c,d)(n:nat) : complex =
    IF n=0 THEN real_(d)
    ELSIF n=1 THEN real_(c)
    ELSIF n=2 THEN real_(b)
    ELSIF n=3 THEN real_(a)
    ELSE real_(0)
    ENDIF

  cpoly_cubic_real : LEMMA
    FORALL (n:nat):
      cpolynomial_real?(n)(cpoly_cubic(a,b,c,d))

  c_cubic_eq_cpolynomial_cubic : LEMMA
    c_cubic(a,b,c,d) = cpolynomial(cpoly_cubic(a,b,c,d),3)

%vieta

  cubic_iff_reduced : LEMMA
    FORALL (x:complex):
      c_cubic(a,b,c,d)(x) = real_(0) IFF
      c_cubic(1,b/a,c/a,d/a)(x) = real_(0)

  depressed(p,q)(t:complex) : complex =
    t^3 + p*t + q 

  cubic_iff_depressed : LEMMA
    FORALL (x,t:complex):
      x = t-b/(3*a) IMPLIES
        (c_cubic(a,b,c,d)(x) = real_(0) IFF
         depressed(P(a,b,c),Q(a,b,c,d))(t) = real_(0))

  vieta(p,q)(w:nzcomplex) : complex =
    w^3 + q - p^3/(27*w^3)

  depressed_iff_vieta : LEMMA
    FORALL (t:complex,w:nzcomplex):
      t = w-p/(3*w) IMPLIES
        (depressed(p,q)(t) = real_(0) IFF
         vieta(p,q)(w) = real_(0))

  vieta_iff_quadratic : LEMMA
    FORALL (w:nzcomplex):
      vieta(p,q)(w) = real_(0) IFF
      c_quadratic(1,q,-p^3/27)(w^3) = real_(0)

  w_exists : LEMMA
    FORALL (t:complex,p:nzreal) :
      EXISTS (w:nzcomplex) : t = w-p/(3*w)

  x_from_t : LEMMA
    FORALL (x,t:complex) :
      x+b/(3*a) = t IFF x = t-b/(3*a)
  
  w_from_t : LEMMA
    FORALL (t:complex,w:nzcomplex):
      t = w-p/(3*w) AND real_?(t) IMPLIES
      c_quadratic(3,-3*Re(t),-p)(w) = real_(0)     
  
  cubic_iff_quadratic : THEOREM
    LET p = P(a,b,c),
        q = Q(a,b,c,d) IN
      FORALL (x:complex) :
        LET t = x+b/(3*a) IN
          c_cubic(a,b,c,d)(x) = real_(0) IFF
	  IF p = 0 THEN t^3 = real_(-q)
          ELSE
	    FORALL (w:nzcomplex):
             t = w-p/(3*w) IMPLIES
             c_quadratic(1,q,-p^3/27)(w^3) = real_(0)
	  ENDIF

END vieta
