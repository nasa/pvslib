complex_root : THEORY
BEGIN

  IMPORTING fundamental_algebra,
            power@root

  r:     VAR real
  z:     VAR complex
  n0z:   VAR nzcomplex
  theta: VAR gt_npi_le_pi
  n:     VAR posnat
  
  c_root(z,n):{x:complex|x^n=z AND ((Im(z)=0 AND odd?(n)) IMPLIES Im(x)=0)} =
    IF Im(z)=0 AND Re(z)<0 AND odd?(n) THEN real_(root(Re(z),n))
    ELSE root_complex(z)(n)
    ENDIF

  c_root_zero: LEMMA
    c_root(z,n)=complex_(0,0) IFF z=complex_(0,0)

  c_root_enum: LEMMA
    FORALL ((z|Im(z)=0 AND Re(z)<0),(n|odd?(n) AND n>1)):
    c_root(z,n)=root_complex_enum(z)(n,(n-1)/2)

  c_root_real_root: LEMMA
    FORALL (r,(n|r>=0 OR odd?(n))):
    c_root(real_(r),n)=real_(root(r,n))

  c_root_root_complex: LEMMA
    IF Im(z)=0 AND Re(z)<0 AND n>1 AND odd?(n) THEN c_root(z,n)=root_complex_enum(z)(n,(n-1)/2)
    ELSE c_root(z,n)=root_complex(z)(n)
    ENDIF

  c_root_mult: LEMMA
    FORALL(z1,z2:complex) : EXISTS (m:nat | m<=n) :
    c_root(z1,n)*c_root(z2,n) = root_of_unity(n)(m)*c_root(z1*z2,n)
  
END complex_root
