riemann_examples : THEORY
  BEGIN

  IMPORTING Riemann@strategies

  %% Examples demonstrating the use of riemann-numerical

  % More precision can be obtained by increasing the PRECISION argument, but
  % this will slow down the strategy.

  % The default value of BREAKS is 12. Using fewer can speed up the strategy
  % but it will give less precise results

  cos_test:LEMMA integral(0,1,cos) ## [|0.841 , 0.843|] 
  %|- cos_test : PROOF
  %|- (riemann-numerical :breaks 9 :precision 2)
  %|- QED
  
  cos_test1:LEMMA integral(0,1,cos) ## [|0.8414 , 0.8417|]
  %|- cos_test1 : PROOF
  %|- (riemann-numerical)
  %|- QED
  
  cos_test5:LEMMA integral(0,1/230,cos) ## [|0.00434781236,0.00434781241|]
  %|- cos_test5 : PROOF
  %|- (riemann-numerical)
  %|- QED
  
  sin_test1:LEMMA integral(0,1.4142135,sin) ## [|0.843 , 0.8449|]
  %|- sin_test1 : PROOF
  %|- (riemann-numerical)
  %|- QED
  
  sin_test:LEMMA integral(0,1/30,sin) ## [|0.000555 , 0.000556|]
  %|- sin_test : PROOF
  %|- (riemann-numerical :breaks 13 :precision 4)
  %|- QED
  
  sin_test2:LEMMA integral(0,2,sin) ## [|1.4158 , 1.4165|]
  %|- sin_test2 : PROOF
  %|- (riemann-numerical)
  %|- QED
  
  sin_test4:LEMMA integral(-1.5,1,sin) ## [|-0.474 , -0.468|]
  %|- sin_test4 : PROOF
  %|- (riemann-numerical)
  %|- QED
  
  exp_test3:LEMMA integral(0,3,exp) ## [|19.0 , 19.1|]
  %|- exp_test3 : PROOF
  %|- (riemann-numerical :precision 3)
  %|- QED

  expcos : LEMMA integral(0, 1, exp * cos) ## [| 1.377, 1.379 |]
  %|- expcos : PROOF
  %|- (riemann-numerical)
  %|- QED

  sin_half_period : LEMMA integral(0, 3.14159, sin) ## [| 1.99, 2.01 |]
  %|- sin_half_period : PROOF
  %|- (riemann-numerical :breaks 10)
  %|- QED

  % idr can be used to refer to the integration variable

  square : LEMMA integral(0, 2, 3 * idr ^ 2) ## [| 7.99, 8.01 |]
  %|- square : PROOF
  %|- (riemann-numerical :breaks 11)
  %|- QED

  % The "o" operator can be used for composition. It is best to explicitly import
  % function_props when doing this even though it's in the prelude because it
  % allows you to fully instantiate o

  IMPORTING function_props[real, real, real]

  comp : LEMMA integral(0, 1, exp o cos) ## [| 2.33, 2.35 |]
  %|- comp : PROOF
  %|- (riemann-numerical :breaks 10)
  %|- QED

  %|- *_TCC1 : PROOF
  %|- (prove-integrable?)
  %|- QED

END riemann_examples
