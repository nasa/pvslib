%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%	        André Camapum Carvalho de Freitas
%% 		Universidade Federal de Goiás - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%	Formalization of results about the ring of integers and
%%	the quotient ring Z/nZ
%%
%%	Last version: October 7, 2020
%%
%%----------------------------------------------------------------------------

ring_zn: THEORY
  BEGIN

   IMPORTING algebra@ring_with_one_def,
   	     algebra@quotient_rings_with_one[int,+,*,0,1],
	     finseq_rel_prime,
	     ints@primes,
	     algebra@field_def,
	     algebra@ring_nz_closed_aux,
	     algebra@division_ring_extras,
	     ints@mod_div_lems,
	     algebra@integral_domain_with_one_def,
	     ints@lcm

            d,n,m: VAR posnat
      a,b,c,x,y,k: VAR int
           fs,fsn: VAR finseq[posnat]
	      fsz: VAR finseq[nzint]

%%----------------------------------------------------------------------------

  nZ(n): set[int] = {x | EXISTS k: x=k*n}

  nZ_add(n)(x,y:(nZ(n))): (nZ(n)) = x + y

  fsnZ(fsn): finseq[set[int]] =
       (# length := length(fsn),
             seq := (LAMBDA (i:below[length(fsn)]): nZ(fsn(i))) #)

%%----------------------------------------------------------------------------

  Z_ring: LEMMA ring?[int,+,*,0](fullset[int]);

  Z: ring[int,+,*,0] = fullset[int]

  Z1_is_Z: LEMMA nZ(1) = Z

  Z_commutative_ring_w_one: LEMMA commutative_ring_with_one?[int,+,*,0,1](Z)

  nZ_ideal: LEMMA FORALL n: ideal?(nZ(n),Z)

  Z_nz_closed: LEMMA nz_closed?[int,+,*,0](Z)

  Z_integral_domain_w_one: LEMMA integral_domain_w_one?[int,+,*,0,1](Z)

  Z(n): setofsets[int] = Z/nZ(n)

  Zn_finite_set: LEMMA is_finite(Z(n))

  Zn_card_n: LEMMA card(Z(n)) = n

  Zn_commutative_ring_w_one: LEMMA
    commutative_ring_with_one?
         [coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n),+(1,nZ(n))]
	 (Z(n))

  equal_cosets_div: LEMMA a + nZ(n) = b + nZ(n) IFF divides(n,b-a)

  nZ_mZ_sum: LEMMA sum(nZ(n),nZ(m)) = nZ(gcd(n,m))

  nZ_mZ_intersection: LEMMA intersection(nZ(n),nZ(m)) = nZ(lcm(n,m))

  nZ_mZ_rel_prime_intersection: COROLLARY
    rel_prime(n,m) IMPLIES intersection(nZ(n),nZ(m)) = nZ(n*m)

  Zn_charac: LEMMA
        FORALL(n:posnat): Z(n) = {y: setof[int] | EXISTS (a: below[n]) : y = +(a,nZ(n))}

  Z2_charac: COROLLARY Z(2) = {y: setof[int] | y = +(0, nZ(2)) OR y = +(1, nZ(2))}

  Zp_prime_is_nz_closed: LEMMA
      FORALL(n:posnat): (prime?(n) IMPLIES
         nz_closed?[coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n)](Z(n)))

  Zp_nz_closed_is_prime_or_one: LEMMA
      FORALL(n:posnat): (nz_closed?[coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n)](Z(n))
         IMPLIES (prime?(n) OR n = 1))

  Zp_prime_is_division_ring: LEMMA
      FORALL(n:posnat): (prime?(n) IMPLIES
         division_ring?[coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n),+(1,nZ(n))](Z(n)))

  Zp_prime_is_field: THEOREM
      FORALL(n:posnat): (prime?(n) IFF field?[coset(Z,nZ(n)),add(Z,nZ(n)),product(Z,nZ(n)),nZ(n),+(1,nZ(n))](Z(n)))

  nZ_mZ_subset: LEMMA subset?(nZ(n),nZ(m)) IFF divides(m,n)

  power_sum_nat: LEMMA group.^(x,n) = n*x

  power_sum_int: LEMMA group.^(x,k) = k*x

  nZ_is_cyclic: LEMMA nZ(n) = generated_by(n)

  mZ_nZ_is_cyclic: LEMMA
     divides(m,n) IMPLIES
                    nZ(m)/nZ(n) = generated_by[coset(nZ(m),nZ(n)),add(nZ(m),nZ(n)),nZ(n)](m+nZ(n))


END ring_zn
