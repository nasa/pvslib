%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%	The main result in this theory is the Chinese Remainder Theorem
%%  	for integers. 
%%  	The interesting point of this formalization is that it arises
%%  	as a Corollary of the algebraic version of the CRT using a
%%  	additional lemmas for integers.
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

chinese_remainder_theorem_Z: THEORY

  BEGIN
    
    IMPORTING ring_zn,
	      algebra@quotient_rings_with_one[int,+,*,0,1],
	      algebra@ring_homomorphisms_def,
	      algebra@chinese_remainder_theorem_rings[int,+,*,0,1],
	      sets_aux@card_finite

            d,n,m: VAR posnat
      a,b,c,x,y,k: VAR int
           fs,fsn: VAR finseq[posnat]
	      fsz: VAR finseq[nzint]
  
%-----------------------------------------------------------------------------

  nZ_mZ_comaximal: LEMMA comaximal_ideals?(Z)(nZ(n),nZ(m)) IFF rel_prime(n,m)

  Intersection_add_first: LEMMA
  FORALL (A: setof[int], fsS: finseq[setof[int]]): 
    Intersection(seq2set(add_first(A, fsS))) = intersection(A, Intersection(seq2set(fsS)))

  nZ_fs_intersection: LEMMA
  (length(fsn) /= 0 AND fs_rel_prime?(fsn))
     IMPLIES
       LET fsnZ: finseq[set[int]] = (# length := length(fsn),
                                           seq:= (LAMBDA (i:below[length(fsn)]): nZ(fsn(i))) #) IN
       Intersection(seq2set(fsnZ)) = nZ(product(fsn))

%-----------------------------------------------------------------------------
%%
%%	 Main Theorem: Chinese Remainder Theorem for integers
%%	 The proof uses  nZ_fs_intersection, algebraic version of the CRT,
%%       nZ_ideal, nz_mz_comaximal, and Z_commutative_ring_w_one
%%
%-----------------------------------------------------------------------------

  Chinese_Remainder_Theorem_for_int: COROLLARY
   FORALL (fspn: finseq[posnat] | length(fspn) /= 0 AND fs_rel_prime?(fspn)):
     R_isomorphic?[coset(Z, nZ(product(fspn))),
                   add(Z, nZ(product(fspn))),
   		   product(Z, nZ(product(fspn))),
   		   nZ(product(fspn)),
	   (cartesian_product_n[set[int]](fsQ(Z)(fsnZ(fspn)))), Sfs(Z)(fsnZ(fspn)), Pfs(Z)(fsnZ(fspn)),fsnZ(fspn)]
   	 (Z/nZ(product(fspn)), cartesian_product_n[set[int]](fsQ(Z)(fsnZ(fspn))))


  gcd_lcm_property: COROLLARY lcm(n,m) = n*m/gcd(n,m)
  
  END chinese_remainder_theorem_Z

%-----------------------------------------------------------------------------
