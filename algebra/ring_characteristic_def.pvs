%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%	Definition of the characteristic of a ring and formalization of
%%	related properties
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

ring_characteristic_def[T:Type+,+,*:[T,T->T],zero:T] : THEORY

 BEGIN

   ASSUMING IMPORTING ring_def[T,+,*,zero]

        fullset_is_ring: ASSUMPTION ring?(fullset[T])

   ENDASSUMING

 IMPORTING ring_basic_properties[T,+,*,zero]

 R: VAR (ring?)
 x: VAR T
 n: VAR posnat
 k: VAR int
 r: VAR nat

%%----------------------------------------------------------------------------
%%	Definition of characteristic of a ring
%%	and some related results
%%----------------------------------------------------------------------------
 
 N(R): set[posnat] = {n | FORALL (x:(R)): times(x,n) = zero}

 charac(R): nat = IF empty?(N(R)) THEN 0
 	    	  ELSE min(N(R))
		  ENDIF

 times_char: LEMMA
     FORALL (x:(R)): times(x, charac(R)) = zero

 member_N_or_zero: LEMMA
     (FORALL (x:(R)): times(x,r) = zero)
      IMPLIES (r = 0 OR member(r,N(R)))

 multiple_char: LEMMA
     (EXISTS (m:int): k = m * charac(R))
      IFF (FORALL (x:(R)): times(x, k) = zero)

 char_1_zero_ring: LEMMA
     charac(R) = 1 IFF R = singleton(zero)

 END ring_characteristic_def
%%----------------------------------------------------------------------------
