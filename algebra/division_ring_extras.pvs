%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		André Camapum Carvalho de Freitas
%%		Universidade Federal de Goiás - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of some results about division ring.
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

division_ring_extras[T:Type+,+,*:[T,T->T],zero:T,one:T]: THEORY

 BEGIN

   ASSUMING IMPORTING

    	ring_nz_closed_def[T,+,*,zero],
	ring_with_one_def[T,+,*,zero,one]
    
 	fullset_is_nz_closed: ASSUMPTION nz_closed?[T,+,*,zero](fullset[T])
	fullset_is_ring_with_one: ASSUMPTION ring_with_one?[T,+,*,zero,one](fullset[T])
	
   ENDASSUMING

 IMPORTING ring_unit[T,+,*,zero,one],
	   division_ring_def[T,+,*,zero,one],
	   ring_w_one_xyx_is_x[T,+,*,zero,one]

 S: VAR ring_with_one
  
%%----------------------------------------------------------------------------

 xyx_division_ring: LEMMA
    FORALL(S:xyx_R): S /= singleton(zero) IMPLIES division_ring?(S)

 div_ring_nz_unit: LEMMA
    division_ring?(S) IFF (S /= singleton(zero) AND
                          FORALL (x: (remove(zero,S))): unit?(S)(x))

 no_prop_l_ideal_div_ring: LEMMA
   (S /= singleton(zero) AND
    FORALL (I: left_ideal(S)): (I = singleton(zero) OR I=S))
   IMPLIES division_ring?(S) 

 no_prop_r_ideal_div_ring: LEMMA
   (S /= singleton(zero) AND
    FORALL (I: right_ideal(S)): (I = singleton(zero) OR I=S))
    IMPLIES division_ring?(S) 

 div_ring_no_prop_ideal: LEMMA
   division_ring?(S) IMPLIES
   (FORALL (I: ideal(S)): I /= singleton(zero) IMPLIES I = S)

 END division_ring_extras
%%---------------------------------------------------------------------------
