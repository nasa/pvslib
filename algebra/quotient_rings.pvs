%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties about quotient rings
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

quotient_rings [T:type+, +:[T,T->T], *:[T,T->T], zero:T]
		: THEORY

  BEGIN

    ASSUMING IMPORTING ring_def[T,+,*,zero]
    
	fullset_is_ring: ASSUMPTION ring?(fullset[T])

    ENDASSUMING
  
    IMPORTING ring_cosets_lemmas[T,+,*,zero],
    	      ring_cosets_lemmas,
              product_coset_def[T,+,*],
	      ring


  R: VAR ring
  S, I: VAR set[T]
  a: VAR T
  A: VAR set[T]
  m: VAR int
  i: VAR nat
  
%-------------------------------------------------


  add_charac: LEMMA
     FORALL (R: ring, I: ideal(R), a,b: (R)):
        add(R,I)(a+I,b+I) = (a+b) + I

  add_is_coset: LEMMA
     FORALL (R: ring, I: ideal(R), A, B: coset(R, I)):
        EXISTS (a: (R)) : add(R,I)(A,B) = a + I

  coset_add: LEMMA
     FORALL (R: ring, I: ideal(R), A, B: coset(R, I)):
        coset?(R,I)(add(R,I)(A,B))

  product_charac: LEMMA
     FORALL (R: ring, I: ideal(R), a,b: (R)):
        product(R,I)(a+I,b+I) = (a*b) + I
  
  lprod_equal_rprod: LEMMA
     FORALL (R: ring, I: ideal(R), A,B: coset(R,I)):
        lproduct(R,I)(A,B) = rproduct(R,I)(A,B)

  product_is_coset: LEMMA  
     FORALL (R: ring, I: ideal(R), A, B: coset(R, I)):
        EXISTS (a: (R)) : product(R,I)(A,B) = a + I

  coset_product: LEMMA
     FORALL (R: ring, I: ideal(R), A, B: coset(R, I)):
        coset?(R,I)(product(R,I)(A,B))

  quotient_group_is_abelian_group: LEMMA
     FORALL(R: ring, I: ideal(R)):
         abelian_group?[coset(R,I), add(R,I), I](R/I)

  quotient_group_is_ring: LEMMA
     FORALL(R: ring, I: ideal(R)):
        ring?[coset(R,I), add(R,I), product(R,I), I](R/I)

 fullset_quot_group_is_ring: LEMMA
     FORALL(R: ring, I: ideal(R)):
        ring?[coset(R,I), add(R,I), product(R,I), I](fullset[coset(R,I)])

 inv_charac: LEMMA
     FORALL (R:ring, I:ideal(R), a:(R)):
        inv[coset(R,I),add(R,I),I](a+I) = inv(a) + I

 coset_subring: LEMMA
     subring?(S,R) AND ideal?(I,R) IMPLIES
       subring?[coset(R,I),add(R,I),product(R,I),I](S/I,R/I)

 coset_ideal: LEMMA
     ideal?(S,R) AND ideal?(I,R) IMPLIES
       ideal?[coset(R,I),add(R,I),product(R,I),I](S/I,R/I)

 commutative_quotient_ring: LEMMA 
      FORALL(R: (commutative_ring?), I: ideal(R)):
        commutative_ring?[coset(R, I), add(R, I), product(R,I), I](R/I)

 lcoset_power_nat: LEMMA
      FORALL(I: ideal(R), a: (R)): 
             ^[coset(R,I), add(R,I), I](a + I,i)  = a^i + I

 lcoset_power_int: LEMMA
      FORALL(I: ideal(R), a: (R)): 
             ^[coset(R,I), add(R,I), I](a + I,m)  = a^m + I

END quotient_rings
%%----------------------------------------------------------------------------
