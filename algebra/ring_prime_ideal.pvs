%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties about prime ideals.
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

ring_prime_ideal [T:TYPE+, +,*: [T,T -> T], zero:T]: THEORY

  BEGIN

   ASSUMING IMPORTING ring_def[T,+,*,zero]

          fullset_is_ring: ASSUMPTION ring?(fullset[T])

   ENDASSUMING

   IMPORTING ring_prime_ideal_def[T,+,*,zero],
   	     ring_one_generator[T,+,*,zero]
	     
	     

  R: VAR ring
  P: VAR set[T]
  a,b: VAR T
  
  prime_ideal_prop1: LEMMA
       (ideal?(P,R) AND P/=R AND
       FORALL (a,b: (R)): member(a*b,P) IMPLIES member(a,P) OR member(b,P))
    IMPLIES
       prime_ideal?(P,R)

  prime_ideal_prop2: LEMMA
       (commutative_ring?(R) AND prime_ideal?(P,R))
    IMPLIES
       FORALL (a,b: (R)): member(a*b,P) IMPLIES member(a,P) OR member(b,P)
    
%%----------------------------------------------------------------------------

 IMPORTING quotient_rings[T,+,*,zero],
           ring_nz_closed_def

   prime_ideal_prod_closed: LEMMA
     FORALL (R:(commutative_ring?), P: prime_ideal(R)):
       FORALL (x,y: coset[T,+](R,P)):
         member(x,remove(P,/[T,+](R,P))) AND member(y,remove(P,/[T,+](R,P)))
	 IMPLIES member(product(R,P)(x,y),remove(P,/[T,+](R,P)))
   
   prime_ideal_nz_closed: LEMMA
     (commutative_ring?(R) AND prime_ideal?(P,R)) IMPLIES
      nz_closed?[coset[T,+](R,P),add[T,+](R,P),product[T,+,*](R,P),P](/[T,+](R,P))

 END ring_prime_ideal


 

   
  


