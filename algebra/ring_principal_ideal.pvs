%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of results about principal ideals and a
%%	characterization of a principal ideal
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

ring_principal_ideal[T:TYPE+, +:[T,T -> T] , *:[T,T -> T], zero:T]: THEORY


 BEGIN

        ASSUMING IMPORTING ring_def[T,+,*,zero]
    
         	fullset_is_ring: ASSUMPTION ring?(fullset[T])

        ENDASSUMING
	

  IMPORTING ring_principal_ideal_def[T,+,*,zero],
	    ring_one_generator[T,+,*,zero]


%%----------------------------------------------------------------------------

  gen_is_member: LEMMA
     FORALL(R: (ring?), a:(R)): member(a, principal_ideal(R)(a))

  principal_ideal_is_ideal: LEMMA
     FORALL(R: (ring?), a:(R)): ideal?(principal_ideal(R)(a), R)

  principal_ideal_charac: LEMMA
     FORALL(R: (ring?), a:(R)): 
         principal_ideal(R)(a) = one_gen(R)(a)
       
  comm_principal_ideal_charac: LEMMA
     FORALL(R:(commutative_ring?), a:(R)):
         principal_ideal(R)(a) = commutative_one_gen(R)(a)

  principal_ideal_subset: LEMMA 
    FORALL(R:(ring?), a:(R), I:ideal(R)): member(a,I) IMPLIES 
     subset?(principal_ideal(R)(a),I)


  stable_chain: LEMMA
   FORALL (R:principal_ideal_ring): 
     FORALL(seq:sequence[set[T]]): 
           ((FORALL(i:nat): ideal?(seq(i),R)) AND 
            (FORALL(i,j: nat): i<j IMPLIES subset?(seq(i),seq(j)))) IMPLIES 
            (EXISTS(n:nat): (FORALL(k:nat | k>=n):seq(k) = seq(n)))

 END ring_principal_ideal
%%----------------------------------------------------------------------------
