%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties involving the relation "divides"
%%
%%	Last version: February 17, 2023
%%
%%----------------------------------------------------------------------------

ring_divides [T: TYPE+, +,*:[T,T->T],zero,one:T ]: THEORY

  BEGIN

   ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]
    
	fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING
 
  IMPORTING ring_divides_def[T,+,*,zero],
  	        ring_with_id_one_generator[T,+,*,zero,one],
	          ring_unit_def[T,+,*,zero,one],
	          integral_domain_def[T,+,*,zero],
	          ring_nz_closed_aux[T,+,*,zero],
	          integral_domain_with_one_def[T,+,*,zero,one],
	          op_finseq_monoid[T,*,one]

   R: VAR ring_with_one
   a, b,u,r,x: VAR T

%%----------------------------------------------------------------------------

  divides_subset: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a:(remove(zero,R)),b:(R)):
       divides?(R)(a,b) IFF subset?(one_gen(R)(b),one_gen(R)(a))

  divides_equal: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a,b:(remove(zero,R))):
       associates?(R)(a,b) IFF one_gen(R)(b) = one_gen(R)(a)

  associates_equiv_relation: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R)):
       equivalence?[(remove(zero,R))](associates?(R))

  unit_divides: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), u:(remove(zero,R))):
       unit?(R)(u) IFF (FORALL(r:(R)): divides?(R)(u,r))

  one_gen_unit_R: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), u:(remove(zero,R))):
       unit?(R)(u) IFF one_gen(R)(u) = R

  quot_unit_associates: LEMMA
   FORALL(R: ring_with_one | commutative_ring?(R), a,b:(remove(zero,R)), r:(R)):
      (unit?(R)(r) AND a = b*r) IMPLIES associates?(R)(a,b)

  int_domain_assoc_quot_unit: LEMMA
   FORALL(R: (integral_domain_w_one?), a,b:(remove(zero,R)), r:(R)):
      (a = b*r AND associates?(R)(a,b)) IMPLIES unit?(R)(r)

  x_divides_x: LEMMA
   FORALL (x:(remove(zero,R))): divides?(R)(x,x)

  int_domain_assoc_unit: LEMMA
    FORALL(R: (integral_domain_w_one?), a,b:(remove(zero,R))):
     associates?(R)(a,b) AND unit?(R)(a) IMPLIES unit?(R)(b)

  div_member_fseq_div_op_finseq: LEMMA
    FORALL (R : (commutative_ring_with_one?), fs : finseq[(R)], c:(remove(zero,R)) ) : 
        FORALL (i: below[length(fs)]) : ( divides?(R)(c,fs(i)) IMPLIES divides?(R)(c,op_fseq(fs)) )
  

  END ring_divides
%%----------------------------------------------------------------------------
