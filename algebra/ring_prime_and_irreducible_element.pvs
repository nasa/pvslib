%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  	        Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%
%%	Last version: May 18, 2022
%%
%%----------------------------------------------------------------------------


ring_prime_and_irreducible_element[T: Type+, +,*:[T,T->T], zero,one:T]: THEORY

  BEGIN

 ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]
    
	fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING

  IMPORTING ring_prime_element_def[T,+,*,zero,one],
  	    ring_irreducible_element_def[T,+,*,zero,one],
  	    ring_with_id_one_generator[T,+,*,zero,one],
	    ring_prime_ideal[T,+,*,zero],
	    ring_with_one_ideal[T,+,*,zero,one],
	    ring_divides[T,+,*,zero,one],
	    ring_nz_closed_aux,
	    integral_domain_with_one_def[T,+,*,zero,one],
	    ring_principal_ideal[T,+,*,zero]

 
  % ————————–
  
  %Theorem 3.4, Chapter III, Hungerford
  
  prime_el_iff_prime_ideal: LEMMA
   FORALL(R:(commutative_ring_with_one?), p:(R)): R_prime_element?(R)(p) IFF
      (one_gen(R)(p) /= singleton(zero) AND prime_ideal?(one_gen(R)(p),R))


  el_irred_iff_one_gen_maximal: LEMMA
   FORALL(R:(integral_domain_w_one?), c:(R)):
     LET S = {X:set[T] | (EXISTS (a:(R)): X = principal_ideal(R)(a)) AND X /= singleton(zero) AND  X /= R} IN
     (R_irreducible_element?(R)(c) IFF
     (member(one_gen(R)(c),S) AND FORALL(X: (S)): subset?(one_gen(R)(c), X) IMPLIES one_gen(R)(c) = X))
      

 prime_el_is_irreducible: LEMMA
   FORALL(R:(integral_domain_w_one?), p:(R)): R_prime_element?(R)(p) IMPLIES R_irreducible_element?(R)(p)


 assoc_irreducible_is_irreducible: LEMMA
   FORALL(R:(integral_domain_w_one?), p,c:(remove(zero,R))):
      (associates?(R)(p,c) AND R_irreducible_element?(R)(p)) IMPLIES R_irreducible_element?(R)(c)


 assoc_prime_is_prime: LEMMA
   FORALL(R:(integral_domain_w_one?), p,c:(remove(zero,R))):
      (associates?(R)(p,c) AND R_prime_element?(R)(p)) IMPLIES R_prime_element?(R)(c)


 irreducible_el_divisors_charac: LEMMA
   FORALL(R:(commutative_ring_with_one?), p,c:(remove(zero,R))):
    (R_irreducible_element?(R)(p) AND divides?(R)(c,p)) IMPLIES (associates?(R)(p,c) OR unit?(R)(c)) 
   	     
 % ————————–

 prime_el_divides: LEMMA
   FORALL(R:(integral_domain_w_one?), p:(R), fs:finseq[(R)]):
     R_prime_element?(R)(p) AND divides?(R)(p,op_fseq(fs))
       IMPLIES
         EXISTS (i: below[length(fs)]): divides?(R)(p,fs(i))

 prime_el_divides_last_pos: LEMMA
   FORALL(R:(integral_domain_w_one?), p:(R), fs:finseq[(R)] | length(fs) /= 0):
     R_prime_element?(R)(p) AND divides?(R)(p,op_fseq(fs))
       IMPLIES
         EXISTS (fsl:finseq[(R)] | length(fsl) = length(fs) ):
	  (FORALL (i: below[length(fsl)]): EXISTS (j: below[length(fs)]): fsl(i) = fs(j))
	   AND op_fseq(fsl) = op_fseq(fs)
           AND divides?(R)(p, fsl(length(fsl) - 1))

 irreducible_prod_not_unit: LEMMA
   FORALL(R:(integral_domain_w_one?), fsI:{fsI: fsIr(R) | length(fsI) /= 0}):
     NOT unit?(R)(op_fseq(fsI))

 irreducible_prod_unit_length_0: LEMMA
   FORALL(R:(integral_domain_w_one?), fsI: fsIr(R)):
     unit?(R)(op_fseq(fsI))
       IMPLIES
         length(fsI) = 0

 irreducible_prod_not_zero: LEMMA
   FORALL(R:(integral_domain_w_one?), fsI: fsIr(R)): op_fseq(fsI) /= zero

 prod_unit_irreducible_is_irreducible: LEMMA
   FORALL (R:(integral_domain_w_one?), p,u:(remove(zero,R))):
      R_irreducible_element?(R)(p) AND unit?(R)(u) IMPLIES R_irreducible_element?(R)(u*p)

 prod_unit_irreducible_is_associates: LEMMA
   FORALL (R:(integral_domain_w_one?), p,u:(remove(zero,R))):
      R_irreducible_element?(R)(p) AND unit?(R)(u) IMPLIES associates?(R)(p,u*p) 

 
  END ring_prime_and_irreducible_element
