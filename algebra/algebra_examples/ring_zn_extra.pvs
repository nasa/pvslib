%------------------------------------------------------------------------------
%  Utility theorems for ring zn manipulation
%
%
% Authors:	
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%               Mariano M. Moscato
%               NASA  Langley  Research  Center - US
%
%------------------------------------------------------------------------------

ring_zn_extra : THEORY

  BEGIN 

  IMPORTING ring_zn
  

  n: VAR posnat
  p: VAR (prime?)

 equal_cosets_rem: LEMMA
   FORALL (a, b: int, n: posnat):
     a+nZ(n) = b+nZ(n) IFF rem(n)(a) = rem(n)(b)

 zn_identity: LEMMA
   FORALL (n: posnat, k: int):
       coset?(Z, nZ(n))(k+nZ(n))

 zn_zero_coset_char: LEMMA
   FORALL (n: posnat, k: int):
       k+nZ(n) = nZ(n) <=> divides(n, k) 
       
 zn_add_pair: LEMMA
   FORALL (n: posnat, x: [(coset?(Z,nZ(n))), (coset?(Z,nZ(n)))]):
       coset?(Z, nZ(n))(add(Z, nZ(n))(x))

 zn_add_charac: LEMMA
   FORALL(n:posnat, a, b: int): add(Z, nZ(n))(a+nZ(n), b+nZ(n)) = a+b + nZ(n)

 zn_product_pair: LEMMA
   FORALL (n: posnat, x: [(coset?(Z,nZ(n))), (coset?(Z,nZ(n)))]):
       coset?(Z, nZ(n))(product(Z, nZ(n))(x))

 zn_product_charac: LEMMA
   FORALL(n:posnat, a, b: int): product(Z, nZ(n))(a+nZ(n), b+nZ(n)) = a*b + nZ(n)

 zn_add_group: LEMMA
  FORALL (n: posnat):
      group?[(coset?(Z, nZ(n))), add(Z, nZ(n)), nZ(n)](fullset[(coset?(Z,nZ(n)))])
 
 IMPORTING algebra@ring_characteristic_def

 times_ring_cosets: LEMMA
   FORALL (a: int)(b: posnat):
     times[coset(Z,nZ(p)),add(Z,nZ(p)),
           product(Z,nZ(p)),nZ(p)](a+nZ(p), b) = a*b+nZ(p)

 charac_cosets: LEMMA
     charac[coset(Z,nZ(p)),add(Z,nZ(p)),
            product(Z,nZ(p)),nZ(p)](fullset[coset(Z, nZ(p))]) = p

 END ring_zn_extra
