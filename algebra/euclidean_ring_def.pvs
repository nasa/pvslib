%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  	        Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%  Definition of a Euclidean Ring 
%%
%%	Last version: April 12, 2022
%%
%%----------------------------------------------------------------------------

euclidean_ring_def[T:TYPE+, +:[T,T -> T] , *:[T,T -> T], zero:T]: THEORY

  BEGIN

  IMPORTING ring_def[T,+,*,zero]

  R: VAR set[T]

  euclidean_ring?(R): bool = commutative_ring?(R) AND
   EXISTS (phi: [(difference(R, singleton(zero))) -> nat]): 
    FORALL(a,b: (R)): ((a*b /= zero IMPLIES phi(a) <= phi(a*b)) AND
                       (b /= zero IMPLIES 
                          EXISTS(q,r:(R)): (a = q*b+r AND 
                                           (r = zero OR (r /= zero AND phi(r) < phi(b))))))
 

  euclidean_pair?(R : (euclidean_ring?), phi: [(difference(R, singleton(zero))) -> nat]) : bool =
    FORALL(a,b: (R)): ((a*b /= zero IMPLIES phi(a) <= phi(a*b)) AND
                       (b /= zero IMPLIES EXISTS(q,r:(R)): 
                       (a = q*b+r AND (r = zero OR (r /= zero AND phi(r) < phi(b))))))



euclidean_f_phi?(R : (euclidean_ring?),phi : [(difference(R, singleton(zero))) -> nat] | euclidean_pair?(R,phi))
                (f_phi : [(R) , (remove(zero,R)) -> [(R),(R)]]) : bool = 
                   FORALL (a : (R), b :(remove(zero,R))): IF a = zero THEN f_phi(a,b) = (zero, zero)  
                   ELSE LET div = f_phi(a,b)`1, rem = f_phi(a,b)`2 IN
                     a = div * b + rem AND (rem = zero OR (rem /= zero AND phi(rem) < phi(b)))
                   ENDIF  

                                                        
    
  END euclidean_ring_def
