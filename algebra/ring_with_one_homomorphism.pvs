%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%              Bruno Berto de Oliveira Ribeiro
%%              Universidade de Brasilia - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of the property: An isomorphism maps a field in another
%%	field.
%%
%%	Last version: April 08, 2025
%%
%%----------------------------------------------------------------------------

ring_with_one_homomorphism
    [T1: TYPE+,s1:[T1,T1 -> T1], p1:[T1,T1 -> T1], zero1: T1, one1:T1,
     T2: TYPE+,s2:[T2,T2 -> T2], p2:[T2,T2 -> T2], zero2: T2, one2:T2]: THEORY


BEGIN


    ASSUMING IMPORTING ring_with_one_def

        fullsetT1_is_ring_w_one: ASSUMPTION
	            ring_with_one?[T1,s1,p1,zero1,one1](fullset[T1])

	fullsetT2_is_ring_w_one: ASSUMPTION
	            ring_with_one?[T2,s2,p2,zero2,one2](fullset[T2])
		    
    ENDASSUMING


   IMPORTING ring_with_one_basic_properties,
   	     ring_homomorphism_lemmas[T1,s1,p1,zero1,T2,s2,p2,zero2],
	     field_def,
	     division_ring_extra


   R: VAR ring_with_one[T1,s1,p1,zero1,one1]
   S: VAR ring_with_one[T2,s2,p2,zero2,one2]

%%----------------------------------------------------------------------------


 epi_maps_ones: LEMMA
    FORALL(R,S)(phi: R_epimorphism(R,S)): phi(one1) = one2

 isomorphic_fields: LEMMA
     R_isomorphic?(R,S) IMPLIES
       (field?[T1,s1,p1,zero1,one1](R) IMPLIES field?[T2,s2,p2,zero2,one2](S))

 isomorphic_division_ring: LEMMA
     R_isomorphic?(R,S) IMPLIES
       (division_ring?[T1,s1,p1,zero1,one1](R)
		IMPLIES division_ring?[T2,s2,p2,zero2,one2](S))

 R_isomorphism_alt_def: LEMMA
     FORALL(phi: [(R) -> (S)]):
        R_isomorphism?(R,S)(phi) IFF injective?(phi) AND surjective?(phi)
	                            AND R_homomorphism?(R,S)(phi)

END ring_with_one_homomorphism
