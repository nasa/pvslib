%%----------------------------------------------------------------------------
%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%              Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%	Last version: October 27, 2023
%%
%%----------------------------------------------------------------------------

quaternions[T:Type+,+,*:[T,T->T],zero,one:T,a,b:(remove(zero,fullset[T]))]: THEORY

BEGIN

ASSUMING IMPORTING field_def[T,+,*,zero,one]

       fullset_is_field: ASSUMPTION field?(fullset[T])

   ENDASSUMING


IMPORTING quaternions_def[T,+,*,zero,one,a,b],
          ring_characteristic_def[T,+,*,zero],
          pvs_strategies_lemmas[T,+,*,zero,one],
          ring_nz_closed_aux[T,+,*,zero],
          center_def,
          ring_nz_closed_aux,
          ring_nz_closed,
          op_finseq_monoid,
          field,
          group


%------------------------------------------


q,u,v: VAR quat
c, d, x, y : VAR T
nz_quat: NONEMPTY_TYPE = {q: quat | q /= zero_q} CONTAINING a_q


%------------------------------------------


one_sc_times:  LEMMA FORALL(q:quat): one * q = q          

sc_quat:        LEMMA (c * u) * (d * v) = (c*d) * (u*v)

sc_sqr_i:      LEMMA (c * i) * (d * i) = (c * d) * a_q

sc_sqr_j:      LEMMA (c * j) * (d * j) = (c * d) * b_q

sc_ji_prod:    LEMMA (c * j) * (d * i) =  (c * d) * inv(k)

inv_charac:    LEMMA inv(q) = inv(one)*q

q_inv_inv:    LEMMA inv(inv(q)) = q

quat_negative_times:        LEMMA (inv(q)) * u = inv(q * u)

quat_times_negative:        LEMMA q * (inv(u)) = inv(q * u)

ji_prod_lem:                    LEMMA j * i = inv(i*j)

kk_prod:                    LEMMA k * k = inv(a_q) * b_q

ki_prod:                    LEMMA k * i = inv(a_q) * j

kj_prod:    LEMMA k * j = b_q * i

ik_prod:                    LEMMA i * k = a_q * j

jk_prod:                    LEMMA j * k = inv(b_q) * i
 
basis_quat: LEMMA FORALL (q: quat): q = q`x * one_q + q`y * i + q`z * j + q`t * k

zero_q_plus: LEMMA FORALL(q: quat): zero_q + q = q 

plus_zero_q: LEMMA FORALL(q: quat): q + zero_q = q 

q_is_zero_iff_conj: LEMMA
  q = zero_q IFF conjugate(q) = zero_q

sc_one_q:   LEMMA c * one_q = (c,zero,zero,zero)

sc_one_quat: LEMMA (c * one_q) * u = c * u 

q_prod_charac: LEMMA FORALL (u,v:quat): 
 u * v = (u`x * v`x + u`y * v`y * a + u`z * v`z * b + u`t * v`t * inv(a) * b, 
          u`x * v`y + u`y * v`x + (inv(b)) *  u`z * v`t + b* u`t * v`z,
          u`x * v`z + u`z * v`x +a * u`y * v`t + inv(a) * u`t * v`y,
          u`x * v`t + u`y * v`z + inv(u`z * v`y) + u`t * v`x ); %quaternion product

zero_q_times: LEMMA FORALL(q: quat): zero_q * q = zero_q 

times_zero_q: LEMMA FORALL(q: quat): q * zero_q = zero_q 
 
quat_is_ring_w_one: LEMMA ring_with_one?[quat,+,*,zero_q,one_q](fullset[quat])

quat_x_pure_part_commutes: LEMMA 
      FORALL (q: quat) :  q * pure_part(q) = pure_part(q) * q

pure_quat_charac: LEMMA FORALL (q: quat): charac(fullset[T]) /= 2 IMPLIES
                              ( (q /= zero_q AND member(q, pure_quat)) IFF
                               (NOT member(q, scalar_F) AND member(q * q, scalar_F)) )

red_trace_charac: LEMMA FORALL (q: quat): red_trace(q) = (q`x + q`x, zero, zero, zero)

red_trace_plus: LEMMA FORALL (p,q: quat): red_trace(p + q ) = red_trace(p) + red_trace(q)

red_trace_times_commutes: LEMMA FORALL (p,q: quat): red_trace(p * q ) = red_trace(q * p)

red_norm_charac: LEMMA FORALL (q: quat):
     red_norm(q) = (q`x * q`x + inv(a) * (q`y * q`y) + inv(b) * (q`z * q`z) + (a * b) * (q`t * q`t), zero, zero, zero)

red_norm_one_q: LEMMA red_norm(one_q) = one_q

red_norm_is_scalar:  LEMMA  FORALL (q : quat) : scalar_F(red_norm(q))

conj_conj_quat : LEMMA FORALL(q : quat) :
 conjugate(conjugate(q)) = q 

conj_product_quat : LEMMA FORALL(q, u : quat) :  
     conjugate(q * u) = conjugate(u) * conjugate(q)

q_x_cq_commutes : LEMMA FORALL (q : quat) : 
         q * conjugate(q) = conjugate(q) * q

conj_product_quat_scalar : LEMMA FORALL(s : T, q : quat) :
     conjugate(s * q) = s * conjugate(q)

red_norm_conj: LEMMA FORALL(q:quat):
    red_norm(conjugate(q)) = red_norm(q)

sc_F_commutes: LEMMA FORALL (s: (scalar_F), q: quat):
    s * q  = q * s

center_quat_is_sc_F: LEMMA charac(fullset[T]) /= 2 IMPLIES
    center[(quat),*](fullset[quat]) = scalar_F

sc_F_product_charac: LEMMA FORALL (s,c: (scalar_F)):
    s * c  = (s`x * c`x, zero, zero, zero)

q_x_v_cq : LEMMA FORALL (q:quat, v:(pure_quat)) : 
    pure_quat(q * v * conjugate(q))  

%%-------------------

T_q(q: quat)(v:(pure_quat)): (pure_quat) = q * v * conjugate(q)

T_q_is_linear: LEMMA FORALL (c,d: T, q: quat, v,w: (pure_quat)):
    T_q(q)(c * v + d * w) = c * T_q(q)(v) + d *  T_q(q)(w)

red_norm_prod: LEMMA FORALL (q, v: quat): 
    red_norm(q * v) = red_norm(q) * red_norm(v)

T_q_red_norm_invariant: LEMMA FORALL (q: quat, v:(pure_quat)):
    red_norm(q) = one_q IMPLIES red_norm(T_q(q)(v)) = red_norm(v)

T_q_invariant_red_norm: LEMMA FORALL (c: T, q: quat):
    red_norm(q) = one_q IMPLIES T_q(q)(c * pure_part(q)) = c * pure_part(q) 

%%----------------------------------------------------------------------------
%% The next set of lemmas provide sufficient and necessary conditions that ensure 
%% Ring of Quaternions as a Division Ring
%%----------------------------------------------------------------------------

IMPORTING division_ring_def

nz_red_norm_iff_inv_exist: LEMMA
  (FORALL (q:nz_quat):
   red_norm(q) /= zero_q) IFF   
   inv_exists?[quat,*,one_q](remove(zero_q, fullset[quat]))

div_ring_iff_nz_rednorm: LEMMA
  division_ring?[quat,+,*,zero_q,one_q](fullset[quat]) IFF
  (FORALL(q: nz_quat): red_norm(q) /= zero_q)

inv_q_prod_charac: LEMMA
 division_ring?[quat,+,*,zero_q,one_q](fullset[quat]) IMPLIES 
 (FORALL (q: nz_quat): 
   inv[nz_quat,*,one_q](q) = conjugate(q) * inv[nz_quat,*,one_q](red_norm(q)))

quat_div_ring_aux1: LEMMA
 (FORALL (x,y:T): a*(x*x) + b*(y*y) /= one) IMPLIES
 FORALL (t:T): t*t + inv[T,+,zero](a) /= zero 

quat_div_ring_aux2: LEMMA
(charac(fullset[T]) /= 2 AND (FORALL (x,y:T): a*(x*x) + b*(y*y) /= one)) IMPLIES
 FORALL (t:T): a*(t*t) + b /= zero 

quat_div_ring_char: LEMMA
charac(fullset[T]) /= 2 IMPLIES
((FORALL (x,y:T): a*(x*x) + b*(y*y) /= one) IFF
division_ring?[quat,+,*,zero_q,one_q](fullset[quat]))

%------------------------------------------

END quaternions
