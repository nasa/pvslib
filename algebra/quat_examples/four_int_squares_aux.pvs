%------------------------------------------------------------------------------
% Authors:	
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%               Mariano M. Moscato
%               AMA @ NASA LaRC - USA
%
%------------------------------------------------------------------------------

four_int_squares_aux: THEORY

  BEGIN

  Lagrange_identity: LEMMA
    FORALL (a0, a1, a2, a3, b0, b1, b2, b3: real):
    	   (a0*a0 + a1*a1 + a2*a2+ a3*a3) * (b0*b0 + b1*b1 + b2*b2 + b3*b3) =
	   (a0*b0 - a1*b1 - a2*b2 - a3*b3) * (a0*b0 - a1*b1 - a2*b2 - a3*b3) +
	   (a0*b1 + a1*b0 + a2*b3 - a3*b2) * (a0*b1 + a1*b0 + a2*b3 - a3*b2) +
	   (a0*b2 - a1*b3 + a2*b0 + a3*b1) * (a0*b2 - a1*b3 + a2*b0 + a3*b1) +
	   (a0*b3 + a1*b2 - a2*b1 + a3*b0) * (a0*b3 + a1*b2 - a2*b1 + a3*b0) 

  four_squares_eq_zero: LEMMA
    FORALL (x, y, z, t: real): x*x+y*y+z*z+t*t = 0
       => x = 0 AND y = 0 AND z = 0 AND t = 0

  four_int_squares: set[int] = {n: int| EXISTS (a,b,c,d :int): n = a*a+b*b+c*c+d*d}

  four_squares_prod_closed: LEMMA
    FORALL (n, k:(four_int_squares)): four_int_squares(n*k)

  even_sum_even_sq_sum: LEMMA
    FORALL (x, y: int): even?(x+y) <=> even?(x*x+y*y)

  Euler_trick_lemma: LEMMA
    FORALL (a, b, c, d, n: int):
      a*a+b*b+c*c+d*d = 2*n AND even?(a+b)
      => EXISTS (x, y, z, t: int):
           x*x+y*y+z*z+t*t = n

  Euler_trick: THEOREM
    FORALL (a, b, c, d, n: int):
      a*a+b*b+c*c+d*d = 2*n =>
        EXISTS (x, y, z, t: int):
          x*x+y*y+z*z+t*t = n

  END four_int_squares_aux
