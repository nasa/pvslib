%------------------------------------------------------------------------------
% Authors:	
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%               Mariano M. Moscato
%               AMA @ NASA LaRC - USA
%
%------------------------------------------------------------------------------

zp_diophantine : THEORY

  BEGIN
  
  IMPORTING ints@primes,
	    sets_aux@card_finite
  	    
  prime_nat: JUDGEMENT (prime?) SUBTYPE_OF posnat

  prime_square(p: (prime?)): set[nat] =
  	{x: below[p] | EXISTS(y: below[p]): rem(p)(y*y) = x}

  prime_square_comp(p: (prime?), k: int): set[nat] =
  	{x: below[p] | EXISTS(y: below[p]): rem(p)(-y*y+k) = x}

  bij_prime_square: LEMMA
    FORALL(p: (prime?) | p > 2): card(prime_square(p)) = (p+1)/2

  bij_prime_square_comp: LEMMA
    FORALL(p: (prime?) | p > 2, k: int): card(prime_square_comp(p, k)) = (p+1)/2

  not_disjoint_prime_squares: LEMMA
     FORALL(p: (prime?) | p > 2, k: int): NOT disjoint?(prime_square(p), prime_square_comp(p, k))

  sum_squares_mod_p_eq_int: LEMMA
      FORALL(p: (prime?), k:int): EXISTS(x, y: int): rem(p)(x*x+y*y) = rem(p)(k)
 
  END zp_diophantine
