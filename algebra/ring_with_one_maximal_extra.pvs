%%
%% Authors:
%%		Bruno Berto de Oliveira Ribeiro
%%		Universidade de Brasília - Brazil
%%
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%
%%----------------------------------------------------------------------------
%%
%%	Ring with one maximal with less restriction
%%
%%	Last version: April 8, 2025
%%
%%----------------------------------------------------------------------------


ring_with_one_maximal_extra
  [T:TYPE+, +:[T,T -> T] , *:[T,T -> T], zero:T,one:T] :THEORY


BEGIN


   ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]

          fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

   ENDASSUMING

   IMPORTING ring_with_one_maximal_ideal[T,+,*,zero,one],
             division_ring


 R: VAR ring_with_one
 M,I: VAR set[T]

   greater_left_ideal(R: ring_with_one,
                      J: set[T],
		      x:(R)| not J(x)): set[T] = {t:T| EXISTS(j:(J), r:(R)): t = j+r*x}

   gt_left_ideal_is_greater_set: LEMMA
   	FORALL (x: (R)| not M(x)):
	      subset?(M, greater_left_ideal(R, M, x))

   gt_left_ideal_is_subset_R: LEMMA
   	FORALL (x: (R)| not M(x)):
	      subset?(M, R) => subset?(greater_left_ideal(R, M, x), R)


   gt_left_ideal_is_left_ideal: LEMMA
   	FORALL (x: (R)| not M(x)):
	       left_ideal?(M, R) IMPLIES left_ideal?(greater_left_ideal(R, M, x), R)


   maximal_left_ideal?(M,R):bool = left_ideal?(M,R) AND M/=R AND
                              FORALL(N:left_ideal(R)): subset?(M,N) AND
			      subset?(N,R) IMPLIES
			      N = M OR N = R

   maximal_left_gt_left_result: LEMMA
     FORALL (x:(R)| not M(x)):
      maximal_left_ideal?(M,R) IMPLIES
     	R = greater_left_ideal(R, M, x)

   maximal_ideal_charac2: THEOREM
     ideal?(M,R) AND maximal_left_ideal?(M,R) IMPLIES
      division_ring?[coset[T,+](R,M),add[T,+](R,M),product(R,M),M,+[T,+](one,M)]
                    (/[T,+](R,M))

END ring_with_one_maximal_extra
