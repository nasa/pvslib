%%
%% Authors:	
%% 		Thaynara Arielly de Lima
%% 		Universidade Federal de Goiás - Brazil
%%
%%  		Andréia Borges Avelar da Silva
%%		Universidade de Brasília - Brazil
%%			
%%		André Luiz Galdino
%%		Universidade Federal de Catalão - Brazil
%%
%%		Mauricio Ayala-Rincón
%%		Universidade de Brasília - Brazil
%%----------------------------------------------------------------------------
%%
%%	Formalization of properties about quotients of rings with one
%%
%%	Last version: June 25, 2020
%%
%%----------------------------------------------------------------------------

quotient_rings_with_one [T:type+, +:[T,T->T], *:[T,T->T], zero:T, one:T]: THEORY

  BEGIN

    ASSUMING IMPORTING ring_with_one_def[T,+,*,zero,one]
    
	fullset_is_ring_with_one: ASSUMPTION ring_with_one?(fullset[T])

    ENDASSUMING

   IMPORTING quotient_rings[T,+,*,zero],
   	     ring_with_one,
	     ring_with_one_ideal[T,+,*,zero,one]

  i: VAR nat

  quotient_ring_with_one: LEMMA
      FORALL(R: (ring_with_one?), I: ideal(R)):
        ring_with_one?[coset(R, I),add(R, I),product(R,I),I,+(one,I)](R/I)

  fullset_quot_ring_with_one: LEMMA
     FORALL(R: (ring_with_one?), I: ideal(R)):
        ring_with_one?[coset(R,I), add(R,I), product(R,I), I,+(one,I)](fullset[coset(R,I)])

  one_diff_zero_coset: LEMMA
      FORALL(R: (ring_with_one?), I: ideal(R)):
      I/=R IMPLIES I /= one+I

  lcoset_power_prod_nat: LEMMA
      FORALL(R: (ring_with_one?), I: ideal(R), a: (R)): 
             power[coset(R,I), product(R,I),+(one, I)](a + I,i)  = power[T, *, one](a,i) + I
  
  END quotient_rings_with_one
