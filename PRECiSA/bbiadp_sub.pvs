bbiadp_sub: THEORY
BEGIN

  IMPORTING interval_ulp_dp,
  	    float@aerr754dp_sub

  R1,E1,R2,E2: VAR Interval

  aebounddp_sub_ia(R1,E1,R2,E2): Interval =
    If NonNeg?(E1) AND NonNeg?(E2)
    THEN Add(Add(E1,E2),Div(Ulp(Add(Abs(Sub(R1,R2)),Add(E1,E2))),[|2|]))
    ELSE EmptyInterval
    ENDIF
 
  re1,re2,re3,re4: VAR RealExpr

  x1,x2,x3,x4: VAR real

  aebounddp_sub_safe(x1,x2,x3,x4): real =
    IF x2 < 0 OR x4 < 0 THEN 1
    ELSE aebounddp_sub(x1,x2,x3,x4) ENDIF

  AEB_SUB_inclusion_j: JUDGEMENT
    aebounddp_sub_ia
    HAS_TYPE (Inclusion4?( Any?,
    	     		   pre1to2b(NonNeg?),
			   Any3?,
			   pre1to4d(NonNeg?),
			   aebounddp_sub_safe ))

  AEB_SUB_fundmental_j: JUDGEMENT
    aebounddp_sub_ia
    HAS_TYPE (Fundamental4?(Any?,pre1to2b(NonNeg?), Any3?,pre1to4d(NonNeg?)))

  AEB_SUB_j: JUDGEMENT
    aebounddp_sub_ia
    HAS_TYPE {F: [[Interval, Interval, Interval, Interval] -> Interval] |
             	   Inclusion4?( Any?,
    	     		   	pre1to2b(NonNeg?),
			   	Any3?,
			   	pre1to4d(NonNeg?),
			   	aebounddp_sub_safe ) (F)
                   AND Fundamental4?( Any?, pre1to2b(NonNeg?), Any3?,
		       		      pre1to4d(NonNeg?) ) (F)}

  AEB_SUB(re1,re2,re3,re4): MACRO RealExpr =
    FUN4(Any?,pre1to2b(NonNeg?),Any3?,pre1to4d(NonNeg?),
	 aebounddp_sub_safe,
	 aebounddp_sub_ia,
	 re1,re2,re3,re4)

END bbiadp_sub
