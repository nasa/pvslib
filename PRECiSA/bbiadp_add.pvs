bbiadp_add: THEORY
BEGIN

  IMPORTING interval_ulp_dp,
  	    float@aerr754dp_add

  R1,E1,R2,E2: VAR Interval

  aebounddp_add_ia(R1,E1,R2,E2): Interval =
    If NonNeg?(E1) AND NonNeg?(E2)
    THEN Add(Add(E1,E2),Div(Ulp(Add(Add(Abs(R1),Abs(R2)),Add(E1,E2))),[|2|]))
    ELSE EmptyInterval
    ENDIF
 
  re1,re2,re3,re4: VAR RealExpr

  x1,x2,x3,x4: VAR real

  aebounddp_add_safe(x1,x2,x3,x4): real =
    IF x2 < 0 OR x4 < 0 THEN 1
    ELSE aebounddp_add(x1,x2,x3,x4) ENDIF

  AEB_ADD_inclusion_j: JUDGEMENT
    aebounddp_add_ia
    HAS_TYPE (Inclusion4?( Any?,
    	     		   pre1to2b(NonNeg?),
			   Any3?,
			   pre1to4d(NonNeg?),
			   aebounddp_add_safe ))

  AEB_ADD_fundmental_j: JUDGEMENT
    aebounddp_add_ia
    HAS_TYPE (Fundamental4?(Any?,pre1to2b(NonNeg?), Any3?,pre1to4d(NonNeg?)))

  AEB_ADD_j: JUDGEMENT
    aebounddp_add_ia
    HAS_TYPE {F: [[Interval, Interval, Interval, Interval] -> Interval] |
             	   Inclusion4?( Any?,
    	     		   	pre1to2b(NonNeg?),
			   	Any3?,
			   	pre1to4d(NonNeg?),
			   	aebounddp_add_safe ) (F)
                   AND Fundamental4?( Any?, pre1to2b(NonNeg?), Any3?,
		       		      pre1to4d(NonNeg?) ) (F)}

  AEB_ADD(re1,re2,re3,re4): MACRO RealExpr =
    FUN4(Any?,pre1to2b(NonNeg?),Any3?,pre1to4d(NonNeg?),
	 aebounddp_add_safe,
	 aebounddp_add_ia,
	 re1,re2,re3,re4)

END bbiadp_add
