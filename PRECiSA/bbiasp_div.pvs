bbiasp_div: THEORY
BEGIN

  IMPORTING interval_ulp_sp,
  	    float@aerr754sp_div

  R1,E1,R2,E2: VAR Interval

  aeb_div_pre4(R1,E1,R2,E2:Interval): bool =
	   NonNeg?(E1) AND
	   NonNeg?(E2) AND
	   Proper?(R2) AND
	   Zeroless?(Sub(Mult(R2,R2),Mult(E2,Abs(R2)))) AND
           (Gt(Sub(R2,E2),0) OR Lt(Add(R2,E2),0))

  aeb_div_pre4_predondition_j: JUDGEMENT
    aeb_div_pre4 HAS_TYPE (Precondition4?)
    
  helper1: LEMMA FORALL(R2:(Proper?),E2: (NonNeg?)):
    (Gt(Sub(R2,E2),0) OR Lt(Add(R2,E2),0)) IMPLIES  Zeroless?(Sub(Abs(R2),E2))
	   
  aeboundsp_div_ia(R1,E1,R2,E2): Interval =
    If aeb_div_pre4(R1,E1,R2,E2) 
    THEN Add(
          Div(
	   Add(Mult(Abs(R2),E1),Mult(Abs(R1),E2)),
	   Sub(Mult(R2,R2),Mult(E2,Abs(R2)))),
          Div(Ulp(Div(Add(Abs(R1),E1),Sub(Abs(R2),E2))),[|2|]))
         
    ELSE EmptyInterval
    ENDIF
 
  re1,re2,re3,re4: VAR RealExpr

  r1,e1,r2,e2: VAR real

  aeboundsp_div_safe(r1,e1,r2,e2): real =
    IF e1 >= 0 AND e2 >= 0 AND (0<r2-e2 OR r2+e2<0)
    THEN aeboundsp_div(r1,e1,r2,e2)
    ELSE 42 ENDIF

  AEB_DIV_inclusion_j: JUDGEMENT
    aeboundsp_div_ia
    HAS_TYPE (Inclusion4?( Any?,
    	     		   Any2?,
			   Any3?,
			   aeb_div_pre4,
			   aeboundsp_div_safe ))
               
  AEB_DIV_fundmental_j: JUDGEMENT
    aeboundsp_div_ia
    HAS_TYPE (Fundamental4?(Any?,Any2?, Any3?,aeb_div_pre4))

  AEB_DIV(re1,re2,re3,re4): MACRO RealExpr =
    FUN4(Any?,Any2?,Any3?,aeb_div_pre4,
	 aeboundsp_div_safe,aeboundsp_div_ia,re1,re2,re3,re4)

END bbiasp_div
