ieee754_operation_scheme__unary
[
  fpnumber   : TYPE,               % floating-point data
  numeric?   : [fpnumber->bool],   % identifies numeric data (finite or infinite)
  zero?      : [fpnumber->bool],   % identifies zeros
  finite?    : [fpnumber->bool],   % identifies floating-point finite numbers
  projection : [(finite?)->real],  % projection function
  rounding   : [real->fpnumber],   % rounding function

  pre?  : [real->bool],         % precondition for the argument
  fun_r : [x:(pre?)->real],    % function on real numbers

  non_finite_behaviour :
  [{arg: fpnumber | NOT finite?(arg) } -> fpnumber],

  undefined_behaviour
  : [ {arg: fpnumber | finite?(arg) AND NOT pre?(projection(arg)) } -> fpnumber ],

  zero_behaviour
  : [ {arg: (finite?) | zero?(arg)} ->fpnumber ]
  
]: THEORY
BEGIN
  ASSUMING

    finite_is_numeric: ASSUMPTION FORALL(X:fpnumber): finite?(X) IMPLIES numeric?(X)

  ENDASSUMING

  fun_fp( X: fpnumber ) : fpnumber
  = IF NOT finite?(X)
    THEN non_finite_behaviour(X)
    ELSIF zero?(X) 
    THEN zero_behaviour(X)
    ELSIF NOT pre?(projection(X)) 
    THEN undefined_behaviour(X)
    ELSE rounding(fun_r(projection(X))) ENDIF

END ieee754_operation_scheme__unary
