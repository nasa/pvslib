extended_float_qge
[ radix     : above(1),
  precision : above(1),
  dExp      : { d: posnat | d > 2*(precision-1) - 1 }
] : THEORY BEGIN

  IMPORTING extended_float_rounding[radix,precision,dExp]
  IMPORTING extended_float_operations_support[radix,precision,dExp]

  qge_non_finite(args: [efloat,efloat] | NOT is_finite?(args`1) OR NOT is_finite?(args`2)): bool
  = LET x: efloat = args`1, y: efloat = args`2
    IN
    IF nan?(x) OR nan?(y) THEN FALSE
    ELSE (x=y) OR ((x = posinf) OR (y = neginf))
    ENDIF

  qge_zero(args: [(is_finite?), (is_finite?)] | zero?(args`1) OR zero?(args`2)): bool
  = LET x = args`1, y = args`2 IN
    (zero?(x) AND zero?(y)) OR
    IF zero?(x) THEN nznfinite?(y)
    ELSE nzpfinite?(x) ENDIF

  IMPORTING ieee754_predicate_scheme__binary[
  	      efloat,
	      is_numeric?,
	      zero?,
	      is_finite?,
	      prj_ef,
	      rnd_ef,
	      LAMBDA(x,y:real):x>=y,
	      qge_non_finite,
	      qge_zero]

  qge_ed(X,Y: efloat): bool = pred_fp(X,Y)

END extended_float_qge
