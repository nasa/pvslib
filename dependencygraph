#!/usr/bin/env perl
# Script for generating a library dependency graph (requires dot) 
# Based on code written by George Hagen (NASA)

use Getopt::Long;
use File::Basename;
use Cwd;

use Graph;
use Graph::Writer::Dot;

$usageln = "$0 [<option> ...] [<file>]";

sub usage() {
  print <<EOF;
NAME
  $0 -- Generates a library dependency graph for libraries in the current directory.

SYNOPSIS

  $usageln

  File <file> contains the list of libraries to be processed.

OPTIONS

 OUTPUT

  --dot                    generate a dot file. Do not generate PDF unless --pdf is specified
  --pdf                    generate a pdf file (default)
  --svg                    generate a svg file
  --title=<string>         use <string> as the title of the graph
  --out=<outfile>          use <outfile> as name of the output file

 SCOPE the scope of the visited libraries can be controlled with the following options

  --do=<dir1>,..,<dirn>    process libraries <dir1>,...,<dirn> 
  --but=<dir1>,..,<dirn>   do not process libraries <dir1>,...,<dirn>
  --after=<dir>            process all libraries after <dir>, exclusive
  --from=<dir>             process all libraries from <dir>, inclusive
  --before=<dir>           process all libraries before <dir>, exclusive
  --to=<dir>               process all libraries to <dir>, inclusive
  --reach-from=<lib>@<th>  print (only) every reachable theory from <lib@th>
  --centered-in=<lib>      show only elements related directly to <lib> 
  --closure=[lib|theory]   automatically grows the scope in order to 
                           - (lib) include every library reachable from theories in it
                           - (theory) zoom into those theories

 FINE-GRAINED CONTROL

  --top=<topfile>          specify name of the top file directory (default: "top")
  --zoom=<dir1>,...,<dirn> zoom into libraries <dir1>,...,<dirn>

 VISUALIZATION

  --full-path              print library full paths (short paths are shown by default)
  --show-top               show top file in the graph (not shown by default)
  --splines=<mode>         control the way in which edges are drawn:
                           - none: no edges,
                           - line: (straight) line [O],
                           - polyline: connected sequence of line segments [N],
                           - ortho: polyline of axis-aligned segments [N],
                           - curved: arc [O],
                           - splines: a special curve defined piecewise by polynomials [N] (default),
                           [O(N)]: (no) overlaps with vertices (when possible)
                           For details, see Vizgraph documentation
  --rankdir=<mode>         control the direction in which nodes are set:
                           - TB: top to bottom,
                           - BT: bottom to top,
                           - LR: left to right (default),
                           - RL: right to left
  --collection-label="<collection>=<label>"
                           Replace collection names (folder of path, according to the full-path 
                           parameter) with user provided labels. This parameter can be used
                           several times: once for each collection to be relabeled

 EXTRAS

  --force                  force regeneration of .dep files
  --help                   print this message

DESCRIPTION

  This script builds a dot graph of library dependencies. 

  If <file> is not provided, the file name all-theories is assumed.
  Particular developments in <file> can be selected using the options: --do,
  --after, --before, --from, --to. The option --but unselect a list of developments.
  
  If <outfile> is not specified, <file><postfix> is used. The <postfix> depends 
  on library selection option, i.e., --do,--but,--from,--to,--after,--before,--zoom.
  Dependencygraph depends on Graphviz's dot utility to produce a pdf or svg output.  
  
  When the option --zoom is used, the specified developments are zoomed such 
  that the each theory in those developments in represented by a node in the 
  dependency graph.
  
  The --reach-from=<lib>@<th> option allows to print only the theories which are 
  reachable from <lib>@<th>. Note, also, that <lib> will be added to the visible 
  scope if not already part of it. Additionally, dependencygraph will zoom into 
  <lib> as if it were mentioned in the --zoom option. Theories from other libraries 
  are explicitly mentioned in the resulting graph only if they belong to a zoomed 
  library, otherwise only the referenced library is mentioned.

EOF
  exit;
}

sub deslash($)
{
  my $string = shift;
  $string =~ s|/||g;
  return $string;
}

sub trim($)
{
  my $string = shift;
  $string =~ s/^\s+//;
  $string =~ s/\s+$//;
  return $string;
}

my $graph = Graph->new; 

sub library_label {
  my ($string,$short) = @_;
  if ($short || ! $fullpath){
    $string =~ s/^\/.*\/([^\/]+)$/$1/;
  }
  return $string;
}

sub collection_label($){
  my $lib = shift;
  my $result = $lib;
  $result =~ s/^\/.*\/([^\/]+)\/[^\/]+$/$1/;
  if (! $result) {
    $result = getcwd; # it's a local library.
    $result =~ s/^\/.*\/([^\/]+)$/$1/ if ! $fullpath;
  }
  my $user_label = $collection_labels{$result};
  $result = $user_label if $user_label;
  return $result;
}

sub unqualify_library_name($) {
  my $lib = shift;
  if ( $lib =~/^.+\/([^\/]+)$/ ) {
    return $1;
  } else {
    return $lib;
  }
}

sub add_theory_vertex {
  my ($lib,$thy) = @_;

  my $node_id = $lib."@".$thy;

  $graph->set_vertex_attribute($node_id,'label',$thy);
  if ($fullpath) { 
    $graph->set_vertex_attribute($node_id,'group',$lib);
  } else {
    $graph->set_vertex_attribute($node_id,'group',library_label($lib));
  }
  $graph->set_vertex_attribute($node_id,'style','filled');
  $graph->set_vertex_attribute($node_id,'fillcolor','#e6f3ff');

  return ($node_id eq $reach_from);
}

sub add_library_vertex {
  my ($lib) = @_;

  $graph->set_vertex_attribute($lib,'label',library_label($lib,true));
  $graph->set_vertex_attribute($lib,'group',collection_label($lib));
  $graph->set_vertex_attribute($lib,'style','filled');
  $graph->set_vertex_attribute($lib,'fillcolor','#fcfcd7');
  $graph->set_vertex_attribute($lib,'shape','box');
}

sub add_edge {
  my ($libsrc,$thysrc,$libtgt,$thytgt) = @_;
  my $reach_from_theory_found=add_theory_vertex($libsrc,$thysrc);

  my $uname = unqualify_library_name($libtgt);
  if (grep(/^$uname$/,@zoomlist)) {
    $reach_from_theory_found = add_theory_vertex($libtgt,$thytgt) || $reach_from_theory_found;
    $graph->add_edge($libsrc."@".$thysrc,$libtgt."@".$thytgt);
  } else {
    add_library_vertex($libtgt);
    $graph->add_edge($libsrc."@".$thysrc,$libtgt);
  }

  return $reach_from_theory_found;
}

sub add_libraries_edge {
  my ($libsrc,$libtgt) = @_;
  add_library_vertex($libsrc);
  add_library_vertex($libtgt);
  $graph->add_edge($libsrc,$libtgt);
}

# Every vertex in the TARGET graph receive the attributes
# that it holds in the SOURCE graph.
#
sub transfer_vertex_attributes {
  my ($tgt,$src) = @_;

  foreach $tgtv ($tgt->vertices) {
    my $srcatt = $src->get_vertex_attributes($tgtv);
    $tgt->set_vertex_attributes($tgtv,$srcatt);
  }
}

#
# -- Main 
#

GetOptions('dot'=>\$dot,
	   'pdf'=>\$pdf,
	   'svg'=>\$svg,
	   'out=s'=>\$out,
	   'title=s'=>\$title,
           'after=s'=>\$after,
           'before=s'=>\$before,
	   'but=s'=>\@but,
	   'do=s'=>\@do,
	   'zoom=s'=>\@zoom,
           'from=s'=>\$from,
	   'help'=>\$help,
	   'top=s'=>\$top,
           'to=s'=>\$to,
	   'force'=>\$force,
	   'full-path'=>\$fullpath,
	   'show-top'=>\$showtop,
	   'reach-from=s'=>\$reach_from,
	   'centered-in=s'=>\$centered_in,
	   'splines=s'=>\$splines,
	   'closure=s'=>\$closure,
	   'collection-label=s'=>\%collection_labels,
	   'rankdir=s'=>\$rankdir) or exit 1;

die("Error: unkonwn value for splines: $splines.\n") if $splines && ($splines ne "ortho")  && ($splines ne "none") && ($splines ne "line") && ($splines ne "false") && ($splines ne "polyline") && ($splines ne "curved") && ($splines ne "spline") && ($splines ne "true");

$splines="spline" if !$splines;
$rankdir="LR" if !$rankdir;
$after = deslash($after);
$before = deslash($before);
$to = deslash($to);
$from = deslash($from);
$do = deslash(join(',',@do));
$but = deslash(join(',',@but));
$zoom = deslash(join(',',@zoom));
my ($reach_from_library,$reach_from_theory) = split(/@/,$reach_from) if $reach_from;
$pdf = $pdf || (!$dot && !$svg);

usage if $help;

$file = shift;
if (!$file) {
    $file = 'all-theories';
} elsif ( ! -f $file ) {
  die 'File $file not found';
}
die "$usageln\n" if shift; 

@butlist = split /,/,$but;
@dolist = split /,/,$do;
@zoomlist = split /,/,$zoom;

die "$file is a directory\n" if -d $file;
open (INFILE,$file) || die "File $file doesn't exist\n";

# Compute graph name and output file name.
#
my $graphName;
if ($out) {
  my ($base,$path,$type) = fileparse($out,qr{\..*});
  $graphName=$base;
  if ($type eq ".pdf") {
    $svg = 0;
    $pdf = 1;
    $dot = 0;
  } elsif ($type eq ".dot") {
    $svg = 0;
    $dot = 1;
    $pdf = 0;
  } elsif ($type eq ".svg") {
    $svg = 1;
    $dot = 0;
    $pdf = 0;
  } 
} else {
  my ($base,$path,$type) = fileparse($file,qr{\..*});  
  $graphName = "$base";
  my $dol = join('_',@dolist);
  $graphName .= "-$dol" if $dol;
  my $butl = join('_',@butlist);
  $graphName .= "-but_$butl" if $butl;
  $graphName .= "-from_$from" if $from;
  $graphName .= "-to_$to" if $to;
  $graphName .= "-after_$after" if $after;
  $graphName .= "-below_$below" if $below;
  my $zooml = join('_',@zoomlist);
  $graphName .= "-zoom_$zooml" if $zooml;
  $graphName .= "-top_$top" if $top;
  $graphName .= "-rf_$reach_from" if $reach_from;
  $graphName .= "-center_$centered_in" if $centered_in;
  $graphName .= "-closure_$closure" if $closure;
  $graphName .= "-fullpath" if $fullpath;
}
$graphName =~ s/@/AT/g;

$out = $path . $graphName;

# Process input file (all-theories by default)
#
my $all = !($from || $after || @dolist || @zoomlist);
my @all_libs;
while (<INFILE>) {
  $line = $_;
  $line =~ s/\#.*$//;
  $line = trim($line);
  if ($line) {
    @listline = split /:/,$line;
    $lib = trim($listline[0]);
    push(@all_libs,$lib);
    if ($lib && -d $lib) {
      push(@nasalib,$lib);
      if ($lib eq $from) {
	$all = 1;
      } elsif ($lib eq $before) {
	$all = 0;
      }
      if (!grep(/^$lib$/,@butlist) && 
	  ($all || grep(/^$lib$/,@dolist) || grep(/^$lib$/,@zoomlist))) { 
	push(@targets,$lib);
      }
      if ($lib eq $after) {
	$all = 1;
      } elsif ($lib eq $to) {
	$all = 0;
      }
    } elsif ($lib) {
      die "Directory $lib not found\n";
    }
  }
}
close(INFILE);

if ($reach_from) {
  push(@targets, $reach_from_library) if !grep(/^$reach_from_library$/,@targets);
  push(@zoomlist, $reach_from_library) if !grep(/^$reach_from_library$/,@zoomlist);
}

if ($centered_in) {
  push(@targets, $centered_in) if !grep(/^$centered_in$/,@targets);
}

die "Nothing to generate\n" if !@targets;

if ($top) {
  $topfile = $top;
} else {
  $topfile = "top";
}

my @libraries=@targets;


$reach_from = getcwd."/".$reach_from if $reach_from;
$centered_in = getcwd."/".$centered_in if $centered_in;

while (@libraries) {
  $lib = pop(@libraries);
  
  push(@nodes,$lib);

  if ($force || ! -f "$lib/pvsbin/$topfile.dep" ) {
    if ($force) {
      print "Generating $lib/pvsbin/$topfile.dep... ";
    } elsif (! -f "$lib/pvsbin/$topfile.dep") {
      print "File $lib/pvsbin/$topfile.dep not found. Generating it... ";
    }
    my $prove_it_result = `proveit -T -i -q --dependencies $lib/$topfile`;      
    die("Error: could not generate dep file (reported error: $!)\n")
      if !defined($prove_it_result);
    print "done.\n";
  }

  my $fullyQualifiedLibName = getcwd."/".$lib;
  my $librarylabel = library_label($fullyQualifiedLibName);

  my $reach_from_theory_found=0;
  if (-f "$lib/pvsbin/$topfile.dep") {
    open(FILE,"$lib/pvsbin/$topfile.dep");
    $zoom = grep(/^$lib$/,@zoomlist);
    while($line = <FILE>) {
      if ($zoom) {
	if ($line =~/(.+):(.*)/ && ($showtop || $1 ne "$topfile")) {
	  
	  add_theory_vertex($fullyQualifiedLibName,$1);
	  
	  if ($2) {
	    my $lib=$fullyQualifiedLibName;
	    my $from=$1;
	    my $tos=$2;
	    @tolist = split /,/,$tos;
	    foreach $to (@tolist) {
	      if ($to =~/^(.*\/)([^\/]+)\/([^\/]+)$/) { # if it has a slash, it is a fully qualified name

		my $uname = unqualify_library_name($1.$2);
		if ($closure) {
		  if (!grep(/^$uname$/,@libraries) && !grep(/^$uname$/,@libraries) && grep(/^$uname$/,@all_libs)) {
		    push(@libraries, $uname);
		    if ($closure eq "theory" && !grep(/^$uname$/,@zoomlist)) {
		      push(@zoomlist, $uname);
		    }
		  } 
		}

		$reach_from_theory_found = add_edge($lib,$from,$1.$2,$3) || $reach_from_theory_found;
	      } else {
		$reach_from_theory_found = add_edge($lib,$from,$lib,$to) || $reach_from_theory_found;
	      }
	    }
	  }
	}
      } elsif ($line =~/(.+)\/$/) {
	# omit path-only library entry in the dep file
      } elsif ($line =~/(.+)\/$lib\/([^\/]+)$/) {
	# add vertex in case the library is not connected to any other
	add_library_vertex($fullyQualifiedLibName);
      } elsif ($line =~/^(\/.+)\//) {
	my $uname = unqualify_library_name($1);
	if ($closure) {
	  if (!grep(/^$uname$/,@libraries) && !grep(/^$uname$/,@libraries) && grep(/^$uname$/,@all_libs)) {
	    push(@libraries, $uname);
	    push(@zoomlist, $uname) if ($closure eq "theory") && !grep(/^$uname$/,@zoomlist);
	  } 
	}
	add_libraries_edge($fullyQualifiedLibName,$1);
      }
    } 
    close FILE;
  } else {
    die "File $lib/pvsbin/$topfile.dep not found\n";
  }
  die("ERROR in --reach-from option value: theory '$reach_from' is not visible.\n".
      "(HINT: Doublecheck spelling and values for --do, --from, --to, --but options and check if the .dep files are up-to-date.)\n")
    if ($reach_from && ($lib eq $reach_from_library) && !$reach_from_theory_found);
}

# Add graphs attributes
$graph->set_graph_attribute('name',"\"$graphName\"");
$graph->set_graph_attribute('rankdir',$rankdir);
$graph->set_graph_attribute('splines',$splines);
$graph->set_graph_attribute('concentrate',true);

# Print dot file
#
my $writer = Graph::Writer::Dot->new(cluster => 'group');
if ($reach_from || $centered_in) {

#  print "graph $graph \n";

  
  # Compute Shortest Path Tree from $reach_from node.
  my @reachables;
  @reachables = $graph->all_successors($reach_from) if $reach_from; 
  @reachables = $graph->neighbours($centered_in) if $centered_in;

#  print "centered_in $centered_in \n";
#  print "reachables @reachables \n";
  
  push(@reachables, $reach_from) if $reach_from;
  push(@reachables, $centered_in) if $centered_in;

  $graph->set_vertex_attribute($centered_in,'fillcolor','#f7e28d') if $centered_in;
  
  my $tree = $graph->subgraph(\@reachables);

  
  # Copy attributes
  $tree->set_graph_attributes($graph->get_graph_attributes());  
  transfer_vertex_attributes($tree,$graph);
  # Write graph
  $writer->write_graph($tree, "$out.dot");
} else {
  $writer->write_graph($graph, "$out.dot");
}
print "File $out.dot has been generated\n" if $dot;

# Print file
#
if (!$dot || $pdf || $svg) {
  my $outext;
  $outext = "pdf" if $pdf;
  $outext = "svg" if $svg;
  die "To produce $out.$outext, install Graphviz and add it to PATH\n" if !`which dot`;
  rename "$out.$outext", "$out.$outext.bak" if -f "$out.$outext";
  my $dotresult = `dot -T$outext $out.dot -o $out.$outext`;
  die("Error: could not generate dot file (reported error: $!)\n")
    if !defined($dotresult);
  print "File $out.$outext has been generated\n";
} 

# Clean
#
`rm $out.dot` if !$dot;
