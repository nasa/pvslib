
ring_1st_isomorphism_theorem[T: TYPE+, +,*:[T,T -> T], zero: T,
                          D: TYPE+, s,p:[D,D -> D], zerod: D] : THEORY

 BEGIN


       ASSUMING IMPORTING algebra@ring_def

	 fullsetT_is_ring: ASSUMPTION ring?[T,+,*,zero](fullset[T])
	 fullsetD_is_ring: ASSUMPTION ring?[D,s,p,zerod](fullset[D])

       ENDASSUMING

 IMPORTING ring_homomorphism_lemmas,
 	   ring_homomorphism_lemmas[T,+,*,zero,D,s,p,zerod],
 	   quotient_rings[T,+,*,zero]

 R: VAR ring[T,+,*,zero]
 S: VAR ring[D,s,p,zerod]
 H,I: VAR set[T]
  
%%----------------------------------------------------------------------------
%%----- Auxiliary Lemmas for the First Isomorphism Theorem -------------------
%%----------------------------------------------------------------------------

 first_isomorphism_th_aux_1: LEMMA
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
	 AND R_homomorphism?[coset(R,I), add(R, I), product(R, I), I,
   		             D, s, p, zerod](R/I,S)(f)
	 
 first_isomorphism_th_aux_2: LEMMA	
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
 	 AND image(f)(R/I) = image(phi)(R)
 
 first_isomorphism_th_aux_3: LEMMA
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
 	AND
        (R_kernel[coset(R,I), add(R, I), product(R, I), I,
   		             D, s, p, zerod](R/I,S)(f)
	 = R_kernel(R,S)(phi)/I )

 first_isomorphism_th_aux_4: LEMMA
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
         AND (R_epimorphism?[coset(R,I), add(R, I), product(R, I), I,
   		             D, s, p, zerod](R/I,S)(f)
	      IFF R_epimorphism?(R,S)(phi))

  first_isomorphism_th_aux_5: LEMMA
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
 	 AND (R_monomorphism?[coset(R,I), add(R, I), product(R, I), I,
   		             D, s, p, zerod](R/I,S)(f)
	      IFF R_kernel(R,S)(phi) = I)

  first_isomorphism_th_aux_6: LEMMA
   FORALL(phi: R_homomorphism(R,S), I: ideal(R)):
     subset?(I,R_kernel(R,S)(phi)) IMPLIES
     EXISTS(f: [(R/I)->(S)]):
        (FORALL(x:(R)): f(x+I) = phi(x))
 	 AND (R_isomorphism?[coset(R,I), add(R, I), product(R, I), I,
   		             D, s, p, zerod]
			    (R/I,S)(f)
 	      IFF R_epimorphism?(R,S)(phi) AND R_kernel(R,S)(phi) = I)
	      
%%----------------------------------------------------------------------------
%%----- First Isomorphism Theorem --------------------------------------------
%%----------------------------------------------------------------------------

   first_isomorphism_th: LEMMA
    FORALL(phi: R_homomorphism(R,S)):
      R_isomorphic?[coset(R, R_kernel(R,S)(phi)),
                    add(R, R_kernel(R,S)(phi)),
   		    product(R, R_kernel(R,S)(phi)),
   		    R_kernel(R,S)(phi),
   		    D, s, p, zerod]
        (R/R_kernel(R,S)(phi), image(phi)(R))



END ring_1st_isomorphism_theorem

%%----------------------------------------------------------------------------
%%----------------------------------------------------------------------------
%%----------------------------------------------------------------------------

ring_2nd_3rd_isomorphism_theorems[T: TYPE+, +,*:[T,T -> T], zero: T,
                          D: TYPE+, s,p:[D,D -> D], zerod: D] : THEORY

 BEGIN


       ASSUMING IMPORTING algebra@ring_def

	 fullsetT_is_ring: ASSUMPTION ring?[T,+,*,zero](fullset[T])
	 fullsetD_is_ring: ASSUMPTION ring?[D,s,p,zerod](fullset[D])

       ENDASSUMING

 IMPORTING ring_1st_isomorphism_theorem,
 	   quotient_rings[T,+,*,zero],
	   quotient_rings


 R: VAR ring[T,+,*,zero]
 S: VAR ring[D,s,p,zerod]
 H,I,J: VAR set[T]


%%----------------------------------------------------------------------------
%%----- Auxiliary Lemma for the Second Isomorphism Theorem -------------------
%%----------------------------------------------------------------------------

  second_isomorphism_th_ax: LEMMA
     subring?(H,R) AND ideal?(I,R) IMPLIES
       EXISTS (phi: [(H) -> (sum(H,I)/I)]):
       R_epimorphism?[T, +, *, zero,
                      coset(sum(H,I),I),add(sum(H,I),I),product(sum(H,I),I),I]
	  (H, sum(H,I)/I)(phi)
       AND R_kernel[T, +, *, zero,
                   coset(sum(H,I),I),add(sum(H,I),I),product(sum(H,I),I),I]
          (H,sum(H,I)/I)(phi) = intersection(H,I)


%%----------------------------------------------------------------------------
%%----- Second Isomorphism Theorem -------------------------------------------
%%----------------------------------------------------------------------------

   second_isomorphism_th: LEMMA
     subring?(H,R) AND ideal?(I,R) IMPLIES
       R_isomorphic?[coset(H,intersection(H,I)),add(H,intersection(H,I)),
		     product(H,intersection(H,I)),intersection(H,I),
		     coset(sum(H,I),I),add(sum(H,I),I),product(sum(H,I),I),I]
 	 (H/intersection(H,I), sum(H,I)/I)

%%----------------------------------------------------------------------------
%%----- Auxiliary Lemma for the Third Isomorphism Theorem --------------------
%%----------------------------------------------------------------------------

 third_isomorphism_th_ax: LEMMA
   ideal?(I,R) AND ideal?(J,R) AND subset?(J,I) IMPLIES
      EXISTS (phi: [(R/J) -> (R/ I)]):
      R_epimorphism?[coset(R,J),add(R,J),product(R,J),J,
                     coset(R,I),add(R,I),product(R,I),I]
          (R/J, R/I)(phi)
      AND R_kernel[coset(R,J),add(R,J),product(R,J),J,
                   coset(R,I),add(R,I),product(R,I),I]
          (R/J, R/I)(phi) = I/J

%%----------------------------------------------------------------------------
%%----- Third Isomorphism Theorem -------------------------------------------
%%----------------------------------------------------------------------------

 third_isomorphism_th: LEMMA
  (ideal?(I,R) AND ideal?(J,R) AND subset?(J,I)) IMPLIES
      R_isomorphic?[coset[coset(R,J),add(R,J)](R/J,I/J),
                    add[coset(R,J),add(R,J),product(R,J)](R/J,I/J),
		    product[coset(R,J),add(R,J),product(R,J)](R/J,I/J),
		    I/J,
		    coset(R,I),add(R,I),product(R,I),I]
         (/[coset(R,J),add(R,J),product(R,J)](R/J,I/J),R/I)

      
END ring_2nd_3rd_isomorphism_theorems


  
