
ring_cosets_lemmas[T:type+,+:[T,T->T],*:[T,T->T], zero:T] : THEORY


 BEGIN

   ASSUMING IMPORTING algebra@ring_def[T,+,*,zero]
   	    fullset_is_ring: ASSUMPTION ring?(fullset[T])
   ENDASSUMING
   
   IMPORTING cosets_def[T,+],
             ring_ideal[T,+,*, zero]
   
%-----------------------------------------------------------------------------

   R       : VAR ring
   A,S,I,H : VAR set[T]
   a       : VAR T
 
%-----------------------------------------------------------------------------
% The lemma lcoset_iff_rcoset holds independently of the relation
% between S and I, because fullset[T] is a ring in this context.  
%-----------------------------------------------------------------------------
   lcoset_iff_rcoset: LEMMA
      (left_coset?(S,I)(A) IFF right_coset?(S,I)(A))

   lcoset_iff_coset: LEMMA
       (left_coset?(S,I)(A) IFF coset?(S,I)(A))

   lcos_eq_rcos: LEMMA
      FORALL (s:(S)): lcos(S,I)(s) = rcos(S,I)(s)
%-----------------------------------------------------------------------------
 
   self_coset: LEMMA
      subring?(I,R) IMPLIES (a + I = I IFF member(a,I))

   gen_is_any: LEMMA
      FORALL (R:ring, I:ideal(R), A:left_coset(R,I), a:(A)):
         A = lc_gen(R, I)(A) + I AND A = a + I

%-----------------------------------------------------------------------------
% The lemma lcos_eq holds for I: set[T] with zero, because fullset[T] is a
% ring in this context, therefore zero is the identity element for addition
% in [T].    
%-----------------------------------------------------------------------------
  lcos_eq: LEMMA
      FORALL (a,b:T): (a + I = b + I AND I(zero)) IMPLIES
         (EXISTS (h:(I)): a = b + h)

%-----------------------------------------------------------------------------
   lcos_eq2: LEMMA
      FORALL (a,b:T):
         (EXISTS (h:(I)): a = b + h) AND group?[T,+,zero](I)
         IMPLIES a + I = b + I

  lc_gen_eq: LEMMA
     ideal?(I,R) AND R(a) IMPLIES
        EXISTS (h: (I)): lc_gen(R,I)(a+I) = a + h

   ring_lcos_subset: LEMMA
      FORALL(I: ideal(R), a: (R)): subset?(a + I, R)
   
   ring_rcos_subset: LEMMA
      FORALL(I: ideal(R), a: (R)): subset?(I + a, R)
      
   left_zero: LEMMA
      zero + A = A

   right_zero: LEMMA
      A + zero = A

  ideal_is_coset: LEMMA
      FORALL(I:ideal(R)): coset?(R,I)(I)
  
%-----------------------------------------------------------------------------

   sum_subring_ideal: LEMMA
     subring?(H,R) AND ideal?(I,R) IMPLIES subring?(sum(H,I),R)

   sum_ideal_ideal: LEMMA
     ideal?(H,R) AND ideal?(I,R) IMPLIES ideal?(sum(H,I),R)

   sum_is_ideal: LEMMA
     subring?(H,R) AND ideal?(I,R) IMPLIES ideal?(I, sum(H, I))

%%----------------------------------------------------------------------------
 END ring_cosets_lemmas
%%----------------------------------------------------------------------------
