%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  The main result in this theory is the algebraic version of the
%  Chinese Remainder Theorem. 
%  The interesting point of this formalization is that it does
%  not requires commutativity and holds just for rings with unity.
%
%  Authors: Andre L. Galdino, Andreia Borges Avelar,
%           Thaynara de Lima, Mauricio Ayala-Rincon
%
%  Last modified: March 23, 2020
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

chinese_remainder_theorem_rings[T:Type+,+,*:[T,T->T],zero,one:T]: THEORY

BEGIN

    ASSUMING IMPORTING algebra@ring_with_one_def[T,+,*,zero,one]
    
	fullset_is_ring: ASSUMPTION ring?(fullset[T])
	fullset_is_ring_with_one: ASSUMPTION
                                         ring_with_one?(fullset[T])
    ENDASSUMING
  

IMPORTING product_finseq_sets_ring[T,+,*,zero,one],
          cartesian_product_quot_ring[T,+,*,zero,one],
	  ring_1st_isomorphism_theorem

            R: VAR ring[T,+,*,zero]
       fsA, y: VAR finseq[set[T]]

 %----------------------------------------------------------------------------------------------

 oneSet(R, fsA)(i: below[length(fsA)],
               (j: below[length(fsA)] | j /= i)): set[T] = {x:(fsA(i)) | member(one - x,fsA(j))}

 %----------------------------------------------------------------------------------------------

 oneSet_nonempty: LEMMA
    FORALL (R: ring_with_one, fsA: fsICM(R) | length(fsA) > 1,
            i: below[length(fsA)], (j: below[length(fsA)] | j /= i)):
      NOT empty?(oneSet(R, fsA)(i,j))

  surjective_aux_1: LEMMA
    FORALL (R: ring_with_one, fsA: fsICM(R)):
      EXISTS (s: finseq[(R)] | length(s) = length(fsA)):
         FORALL (i: below[length(fsA)]):
            s(i) + fsA(i) = one + fsA(i)  AND
            (FORALL (j: below[length(fsA)] | j /= i):  member(s(i),fsA(j)) )

  surjective_aux_2: LEMMA
  FORALL (R: ring_with_one, fsA: fsICM(R)):
    member(y, cartesian_product_n[set[T]](fsQ(R)(fsA)))
  IMPLIES
  EXISTS (s: finseq[(R)] | length(s) = length(fsA), r: finseq[(R)] | length(r) = length(fsA)):
   FORALL (i: below[length(fsA)]):
    y(i) = r(i) + fsA(i) AND
    R_sigma_below[T,+,*,zero,length(fsA)](0, length(fsA) - 1,
                         (LAMBDA (i: below[length(fsA)]): s(i) * r(i))) + fsA(i) = r(i) + fsA(i)
  
  CRT_aux_1: LEMMA
    FORALL (fsA: fsI(R) | length(fsA) /= 0):
     LET phi = LAMBDA (x: (R)): (# length := length(fsA),
               seq := (LAMBDA (i: below[length(fsA)]): x + fsA(i)) #) IN
     R_homomorphism?[T,+,*,zero,
	             (cartesian_product_n[set[T]](fsQ(R)(fsA))), Sfs(R)(fsA), Pfs(R)(fsA),fsA]
     (R,cartesian_product_n[set[T]](fsQ(R)(fsA)))(phi)
      AND
      R_kernel[T,+,*,zero,
	       (cartesian_product_n[set[T]](fsQ(R)(fsA))), Sfs(R)(fsA), Pfs(R)(fsA),fsA]
      (R,cartesian_product_n[set[T]](fsQ(R)(fsA)))(phi) = Intersection(seq2set(fsA))


  CRT_aux_2: LEMMA
    FORALL (R: ring_with_one, fsA: fsICM(R) | length(fsA) /= 0):
     LET phi = LAMBDA (x: (R)): (# length := length(fsA),
               seq := (LAMBDA (i: below[length(fsA)]): x + fsA(i)) #) IN
     surjective?[(R),(cartesian_product_n[set[T]](fsQ(R)(fsA)))](phi)

% Chinese Remainder Theorem for rings with unity
%
  Chinese_Remainder_Theorem: LEMMA  
   FORALL (R: ring_with_one, fsA: fsICM(R) | length(fsA) /= 0):
     R_isomorphic?[coset(R, Intersection(seq2set(fsA))),
                   add(R, Intersection(seq2set(fsA))),
   		   product(R, Intersection(seq2set(fsA))),
   		   Intersection(seq2set(fsA)),
   		   (cartesian_product_n[set[T]](fsQ(R)(fsA))), Sfs(R)(fsA), Pfs(R)(fsA),fsA]
       (R/Intersection(seq2set(fsA)), cartesian_product_n[set[T]](fsQ(R)(fsA)))

END chinese_remainder_theorem_rings
