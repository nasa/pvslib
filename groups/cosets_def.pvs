
cosets_def[T:type+,+:[T,T->T]] : THEORY

  BEGIN


  G,H,I: VAR set[T]
    g,h: VAR T


%------------------- Special Notation ----------------------------------------

   +(g,H): set[T] = {t:T | EXISTS (h:(H)): t = g+h} ;  % left

   +(H,g): set[T] = {t:T | EXISTS (h:(H)): t = h+g} ;  % right

   sum(H,I): set[T] = {t:T | EXISTS (h:(H), k:(I)): t = h + k}

%------------------- Left Cosets ---------------------------------------------
  
   lcos(G,H)(g:(G)) : set[T] = g+H

   left_coset?(G,H)(A:set[T]) : bool = (EXISTS (a: (G)): A = a+H)

   left_coset(G,H) : TYPE = (left_coset?(G,H))

   lc_gen(G,H)(A:left_coset(G,H)) : T = choose({a: T | G(a) AND A = a + H})

%------------------- Right Cosets --------------------------------------------

   rcos(G,H)(g:(G)) : set[T] = H+g

   right_coset?(G,H)(A:set[T]) : bool = (EXISTS (a: (G)): A = H+a)

   right_coset(G,H) : TYPE = (right_coset?(G,H))

   rc_gen(G,H)(A:right_coset(G,H)) : T = choose({a: T | G(a) AND A = H + a})
       
%------------------- Cosets --------------------------------------------------

   coset?(G,H)(A:set[T]) : bool = left_coset?(G,H)(A) AND right_coset?(G,H)(A)

   coset(G,H) : TYPE = (coset?(G,H))
   
   gen(G,H)(A:coset(G,H)) : T = lc_gen(G,H)(A)

%-----------------------------------------------------------------------------
   lcos_is_left_coset: LEMMA
      FORALL (g: (G)): left_coset?(G,H)(lcos(G,H)(g))

   rcos_is_right_coset: LEMMA
      FORALL (g: (G)): right_coset?(G,H)(rcos(G,H)(g))

%-----------------------------------------------------------------------------
END cosets_def
%-----------------------------------------------------------------------------
