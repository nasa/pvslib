
ring_homomorphism_lemmas[T1: TYPE+,
                              s1:[T1,T1 -> T1], p1:[T1,T1 -> T1], zero1: T1,
                         T2: TYPE+,
                              s2:[T2,T2 -> T2], p2:[T2,T2 -> T2], zero2: T2]
			 : THEORY

  BEGIN

        ASSUMING IMPORTING algebra@ring_def

         R1_is_ring: ASSUMPTION ring?[T1,s1,p1,zero1](fullset[T1])
 	 R2_is_ring: ASSUMPTION ring?[T2,s2,p2,zero2](fullset[T2])

        ENDASSUMING

  IMPORTING ring_basic_properties[T1,s1,p1,zero1],
 	    ring_basic_properties[T2,s2,p2,zero2],
 	    ring_homomorphisms_def[T1,s1,p1,zero1,T2,s2,p2,zero2],
	    ring_homomorphisms_def
 	   
  R1:  VAR ring[T1,s1,p1,zero1]
  R2:  VAR ring[T2,s2,p2,zero2]
  S1:  VAR set[T1]
  S2:  VAR set[T2]
 
%%----------------------------------------------------------------------------

  R_homo_plus: LEMMA
      FORALL(phi: R_homomorphism(R1,R2)):
      FORALL(x,y:(R1)): phi(s1(x,y)) = s2(phi(x),phi(y))

  R_homo_mult: LEMMA
      FORALL(phi: R_homomorphism(R1,R2)):
      FORALL(x,y:(R1)): phi(p1(x,y)) = p2(phi(x),phi(y))

  R_homo_equiv: LEMMA
      FORALL(phi:[(R1) -> (R2)]):
      R_homomorphism?(R1,R2)(phi) IFF FORALL(x,y:(R1)): phi(s1(x,y)) = s2(phi(x),phi(y)) AND phi(p1(x,y)) = p2(phi(x),phi(y))


  zero_to_zero: LEMMA 
      FORALL(phi: R_homomorphism(R1,R2)): phi(zero1) = zero2

  inv_to_inv: LEMMA
      FORALL(phi: R_homomorphism(R1,R2)):
      FORALL(x:(R1)): phi(inv(x)) = inv(phi(x))

  epi_commutative: LEMMA
      FORALL(phi: R_epimorphism(R1,R2)):
      commutative_ring?(R1) IMPLIES commutative_ring?(R2)

  image_homo_is_subring: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)): subring?(image(phi)(R1),R2)

  R_homo_image_subring: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)):
       subring?(S1,R1) IMPLIES subring?(image(phi)(S1), R2)

  R_homo_inv_image_subring: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)):
       subring?(S2,R2) IMPLIES subring?(inverse_image(phi, S2), R1)
  
  R_kernel_is_subring: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)): subring?(R_kernel(R1,R2)(phi),R1)

  R_kernel_is_subgroup: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)): subgroup?(R_kernel(R1,R2)(phi),R1)

  monomorphism_charac: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)):
           (R_monomorphism?(R1,R2)(phi) IFF
 	   R_kernel(R1,R2)(phi) = singleton(zero1))

 inv_iso_is_iso: LEMMA
       FORALL (phi: R_isomorphism(R1,R2)): R_isomorphism?[T2,s2,p2,zero2,T1,s1,p1,zero1](R2,R1)(inverse(phi))


%%----------------------------------------------------------------------------

  IMPORTING ring_ideal[T1,s1,p1,zero1],
  	    ring_ideal[T2,s2,p2,zero2],
	    cosets_def

  R_kernel_is_ideal: LEMMA
        FORALL (phi: R_homomorphism(R1,R2)): ideal?(R_kernel(R1,R2)(phi),R1)

  R_epimorphism_image_ideal: LEMMA
        FORALL (phi: R_epimorphism(R1,R2), I: ideal(R1)):
        ideal?(image(phi)(I), R2)

  R_homo_inv_image_ideal: LEMMA
       FORALL (phi: R_homomorphism(R1,R2), I: ideal(R2)): 
          ideal?(inverse_image(phi, I), R1)

  R_kernel_in_inverse_image: LEMMA
       FORALL (phi: R_homomorphism(R1,R2)):
       subring?(S2,R2) IMPLIES
          LET K = R_kernel(R1,R2)(phi) IN
          subset?(K, inverse_image(phi, S2))

  inv_image_image_sum: LEMMA
        FORALL (phi: R_homomorphism(R1,R2)):
	subring?(S1,R1) IMPLIES
          LET K = R_kernel(R1,R2)(phi) IN
          inverse_image(phi, image(phi, S1)) = sum[T1,s1](S1,K)

  inv_image_image_subring: LEMMA
        FORALL (phi: R_homomorphism(R1,R2)):
	subring?(S1,R1) IMPLIES
          LET K = R_kernel(R1,R2)(phi) IN
          inverse_image(phi, image(phi, S1)) = S1 IFF subring?(K,S1)

%%----------------------------------------------------------------------------

 IMPORTING quotient_rings[T1,s1,p1,zero1],
 	   ring_homomorphisms_def
	   
	   
 S: VAR ring[T1,s1,p1,zero1]
 I: VAR set[T1]

 ring_natural_homo: LEMMA
   FORALL(I:ideal(S)):
   (LET phi:[(S) -> (S/I)] = (LAMBDA (r:(S)): r + I) IN
    R_epimorphism?[T1,s1,p1,zero1,
                   coset(S,I),add(S,I),product(S,I),I](S,S/I)(phi)
    AND I = R_kernel[T1,s1,p1,zero1,
                     coset(S,I),add(S,I),product(S,I), I]
		    (S,S/I)(phi))


 END ring_homomorphism_lemmas
 
 %%----------------------------------------------------------------------------
 
ring_homomorphism_lemmas_extras[T1: TYPE+,s1:[T1,T1 -> T1], p1:[T1,T1 -> T1], zero1: T1,
                                T2: TYPE+, s2:[T2,T2 -> T2], p2:[T2,T2 -> T2], zero2: T2]
			       : THEORY

  BEGIN

        ASSUMING IMPORTING algebra@ring_def

         R1_is_ring: ASSUMPTION ring?[T1,s1,p1,zero1](fullset[T1])
 	 R2_is_ring: ASSUMPTION ring?[T2,s2,p2,zero2](fullset[T2])

        ENDASSUMING

  IMPORTING ring_homomorphism_lemmas,
  	    quotient_rings[T1,s1,p1,zero1]

 S : VAR ring[T1,s1,p1,zero1]

zero_natural_isomorphism: LEMMA
   R_isomorphic?[T1,s1,p1,zero1,
                coset(S,singleton(zero1)),add(S,singleton(zero1)),product(S,singleton(zero1)),singleton(zero1)]
   (S,S/singleton(zero1))

END ring_homomorphism_lemmas_extras
