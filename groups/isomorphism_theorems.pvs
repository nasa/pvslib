
%%-------------------** Isomorphism Theorems **-------------------
%%                                                                          
%% Author          : André Luiz Galdino 
%%                   Universidade Federal de Goiás - Brasil
%%                    
%% Last Modified On: November 28, 2011
%%                                                                          
%%----------------------------------------------------------------


isomorphism_theorems[T1: TYPE,
                    *: [T1,T1 -> T1],
                 one1: T1,
                   T2: TYPE,
                    o: [T2,T2 -> T2],
                 one2: T2]: THEORY

BEGIN
  
   ASSUMING IMPORTING algebra@group_def

       T1_is_group: ASSUMPTION group?[T1,*,one1](fullset[T1])
       T2_is_group: ASSUMPTION group?[T2,o,one2](fullset[T2])

   ENDASSUMING


   IMPORTING homomorphism_lemmas

   G:  VAR group[T1,*,one1]
   GP: VAR group[T2,o,one2]
    S: VAR setofsets[T1]

quotient_subgroup: LEMMA 
      FORALL (M, N:normal_subgroup(G)): 
        subgroup?(N,M)
          IMPLIES subgroup?[left_cosets(G,N),mult(G,N),N](M/N, G/N)


second_isomorphism_th_aux: LEMMA 
  FORALL (H: subgroup(G), N: normal_subgroup(G)):
    LET NH = prod[T1,*,one1](N,H) IN
      EXISTS (varphi: [(H) -> (NH/N)]): 
              homomorphism?(H,NH/N,varphi)                AND 
              surjective?(varphi)                        AND 
              kernel(H, NH/N)(varphi) = intersection(N,H)   


second_isomorphism_th: THEOREM 
  FORALL (H: subgroup(G), N: normal_subgroup(G)):
    LET NH = prod[T1,*,one1](N,H) IN
      normal_subgroup?[T1,*,one1](intersection(N,H),H) AND 
                  isomorphic?(H/intersection(N,H), NH/N)
	
	
third_isomorphism_th_aux: LEMMA 
   FORALL (M, N:  normal_subgroup(G)): 
    subgroup?(N,M)
      IMPLIES
        EXISTS (psi: [(G/N) -> (G/M)]): 
                 homomorphism?(G/N,G/M,psi)    AND 
                 surjective?(psi)              AND 
                 kernel(G/N, G/M)(psi) = M/N   

	       
third_isomorphism_th: THEOREM 
   FORALL (M, N:  normal_subgroup(G)): 
    subgroup?(N,M)
      IMPLIES
        normal_subgroup?[left_cosets(G,N),mult(G,N),N](M/N, G/N)    AND
        isomorphic?((G/N)/(M/N), G/M)



%%%%% Fourth Isomorphism Theorem %%%%%

correspondence_theorem: THEOREM 
        FORALL (f: homomorphism(G,GP)):
           surjective?(f)
              IMPLIES
                EXISTS (phi: [subgroup_contain(G,kernel(G,GP)(f))  -> subgroup(GP)]):
                   phi = (LAMBDA (N: subgroup_contain(G, kernel(G, GP)(f))): image(f, N))	AND
                   bijective?(phi)                                                              AND 
                   (FORALL (N: subgroup_contain(G,kernel(G,GP)(f))):  
                       (subgroup?[T1,*,one1](N,G) IFF subgroup?[T2,o,one2](phi(N), GP))          AND
                       (normal_subgroup?[T1,*,one1](N,G) IFF normal_subgroup?[T2,o,one2](phi(N), GP)))



END isomorphism_theorems
