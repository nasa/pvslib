% This theory is based on the csv python package.
csv_writer_def: THEORY
BEGIN

  IMPORTING csv_utils,
            pvsio_utils@top

  CSVDialect: TYPE = [# dlmt: StringChar,    % separator (delimiter)
  	     	        qchr: StringChar,    % quote char
			cmmt: string #]      % comment string. Empty means no comments allowed 

  csv_dialect(separator,quotechar:StringChar,commentstr:string) : CSVDialect =
    (# dlmt := separator, qchr := quotechar, cmmt := commentstr #)

  csv_dialect(separator,quotechar:StringChar) : CSVDialect =
    csv_dialect(separator,quotechar,"")

  % CSVDialect's observers
  csv_dialect_separator(dialect: CSVDialect): MACRO StringChar = dialect`dlmt
  csv_dialect_quotechar(dialect: CSVDialect): MACRO StringChar = dialect`qchr
  csv_dialect_commentstr(dialect: CSVDialect): MACRO string = dialect`cmmt

  % Predefined dialects
  MSEXCEL_DIALECT: CSVDialect = csv_dialect(",",doublequote)
  SQCOMMA_DIALECT: CSVDialect = csv_dialect(",",singlequote)  

  CSVWriter: TYPE = [# outs: OStream,       % output stream
                       dlct: CSVDialect #]  % csv dialect to be used by the writer

  csv_writer_filename(writer: CSVWriter) : string = fname(writer`outs)
  csv_writer_stream(writer: CSVWriter): OStream = writer`outs 
  csv_writer_dialect(writer: CSVWriter): CSVDialect = writer`dlct
  csv_writer_separator(writer: CSVWriter): StringChar = writer`dlct`dlmt
  csv_writer_quotechar(writer: CSVWriter): StringChar = writer`dlct`qchr
  csv_writer_commentstr(writer: CSVWriter): string = writer`dlct`cmmt

  csv_writer(csvfilename: string, dialect: CSVDialect): CSVWriter =
    (# outs := fopenout(rename,csvfilename),
       dlct := dialect #)

  csv_writer(mode:(output_mode?),csvfilename:string, dialect: CSVDialect): CSVWriter =
    (# outs := fopenout(mode,csvfilename),
       dlct := dialect #)

  % Open CSV file, with a default dialect (MSEXCEL_DIALECT)
  csv_writer(csvfilename: string) : CSVWriter =
    csv_writer(csvfilename,MSEXCEL_DIALECT)

  csv_writer_stdout(dialect: CSVDialect): CSVWriter =
    (# outs := stdout ,
       dlct := dialect #)

  % Write in stdout with a default dialect (MSEXCEL_DIALECT)
  csv_writer_stdout : CSVWriter =
    csv_writer_stdout(MSEXCEL_DIALECT)

  set_separator(writer: CSVWriter, separator: StringChar): CSVWriter =
    writer WITH [`dlct`dlmt := separator]

  set_quotechar(writer: CSVWriter, quotechar: StringChar): CSVWriter =
    writer WITH [`dlct`qchr := quotechar]

  set_commentstr(writer: CSVWriter, commentstr: string): CSVWriter =
    writer WITH [`dlct`cmmt := commentstr]

  close_csv_writer(writer:CSVWriter) : void =
    fclose(writer`outs)
  
END csv_writer_def
