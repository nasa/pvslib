% Complex polynomial with real coefficients
cpolynomial_real : THEORY
BEGIN

  IMPORTING fundamental_algebra,
            reals@more_polynomial_props,
	    analysis@interm_value_thm,
	    analysis@polynomial_deriv

  %  Existence of roots of real polynomials of odd degree
  p : VAR sequence[real]
  x : VAR real
  n : VAR nat

  root_polynomial_odd : THEOREM
    n > 0 AND p(n) /= 0 AND % Polynomial is not constant
    odd?(n)                 % odd degree
    IMPLIES
    EXISTS (x): polynomial(p,n)(x) = 0

  % complex polynomial define in fundamental_algebra
  a : VAR sequence[complex]
  z : VAR complex

  cpolynomial_real?(n)(a) : bool =
    FORALL (i:upto(n)) : real_?(a(i))

  cpolynomial_real_real : LEMMA
    cpolynomial_real?(n)(a) IMPLIES
    real_?(cpolynomial(a,n)(real_(x)))

  cpolynomial_polynomial : LEMMA
    (FORALL (i:upto(n)): a(i) = real_(p(i))) IMPLIES
    cpolynomial(a,n)(real_(x)) = real_(polynomial(p,n)(x))

  real_root_cpolynomial_odd : THEOREM
    cpolynomial_real?(n)(a) AND
    n > 0 AND a(n) /= real_(0) AND % Polynomial is not constant
    odd?(n)                        % odd degree
    IMPLIES
    EXISTS (z): cpolynomial(a,n)(z) = real_(0) AND real_?(z)
  
  cpolynomial_real_conjugate_real : LEMMA
    cpolynomial_real?(n)(a) IMPLIES
    cpolynomial(a,n)(conjugate(z)) = conjugate(cpolynomial(a,n)(z))

  cpolynomial_real_root_conjugate : LEMMA
    cpolynomial_real?(n)(a) IMPLIES
    FORALL (z:complex) :
      cpolynomial(a,n)(z) = real_(0) IMPLIES
      cpolynomial(a,n)(conjugate(z)) = real_(0)

END cpolynomial_real
