%------------------------------------------------------------------------------
%  Proof of the infinity of prime numbers using Fermat numbers
%
%  Formalization based on the reference "Proofs from the Book", 
%  Martin Aigner Günter and M. Ziegler
%
% Authors:	
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%               Mariano M. Moscato
%               AMA @ NASA LaRC - USA
%
%------------------------------------------------------------------------------

inf_prime_Fermat: THEORY

  BEGIN

  IMPORTING ints@number_util

%—————– Fermat numbers

  Fermat_number(n: nat): posnat = expt(2, expt(2, n))+1
 
  Fermat_num_is_odd_gt_2: LEMMA
      FORALL (n: nat): odd?(Fermat_number(n)) AND Fermat_number(n) > 2

  prod_Fermat(n: nat): posnat = product(0, n, Fermat_number)

  prod_Fermat_nums : LEMMA
      FORALL (n: posnat): prod_Fermat(n-1) = Fermat_number(n)-2

  Fermat_divides_prod: LEMMA
      FORALL (n : nat)(k : nat): divides(Fermat_number(n), prod_Fermat(n+k))

  Fermat_gcd_one: THEOREM
      FORALL (n : nat, m : nat | n /= m):
        gcd(Fermat_number(n), Fermat_number(m)) = 1 

  gcd_eq1_diff_primes: LEMMA
      FORALL (n, k: posnat, p1: (prime?) | divides(p1, n),
                            p2: (prime?) | divides(p2, k)):
        gcd(n, k) = 1 => p1 /= p2

%—————– Infinitude of primes

 IMPORTING sets_aux@infinite_nat_def[(prime?)],
           sets_aux@card_sets_lemmas[(prime?)],
           sets_aux@infinite_card[(prime?), (prime?)]

  min_prime_Fermat_nonempty: LEMMA
    FORALL (n: nat):
      nonempty?[(prime?)]({p: (prime?) | divides(p, Fermat_number(n))})

  min_prime_Fermat(n: nat): (prime?) =
      min({p: (prime?) | divides(p, Fermat_number(n))})

  Fermat_min_primes: set[(prime?)]=
      {p: (prime?)| EXISTS(k: nat): p = min_prime_Fermat(k)}

  nat_to_Fermat_primes_injective: LEMMA
    EXISTS (f: [nat->(Fermat_min_primes)]): injective?(f) 

  Fermat_min_primes_is_infinite: LEMMA is_infinite(Fermat_min_primes)

  primes_is_infinite: THEOREM is_infinite(fullset[(prime?)])

  END inf_prime_Fermat
