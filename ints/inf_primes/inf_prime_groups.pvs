%------------------------------------------------------------------------------
%  Proof of the infinity of prime numbers using the multiplicative group
%  Z_q, where q is a prime number. 
%
%  Formalization based on the reference "Proofs from the Book", 
%  Martin Aigner Günter and M. Ziegler
%
% Authors:
%               Marcos Rony Silva Brandão
%               Universidade Federal de Goiás - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
%               Mariano M. Moscato
%               NASA  Langley  Research  Center - US
%
%------------------------------------------------------------------------------

inf_prime_groups: THEORY

BEGIN
IMPORTING ints@max_finite_set_nat,
          ints@number_util,
	  algebra_examples@ring_zn_extra,
          algebra@finite_groups,
          algebra@quotient_rings[int, +, *, 0],
	  algebra@finite_groups,
	  finite_sets@finite_sets_card_eq,
	  numbers@fermats_little_theorem
	  
		  
q: VAR (prime?)   
n: VAR posnat
a: VAR int
i: VAR nat


Mersenne_number(p:(prime?)): posnat = expt(2, p)-1

%——- nz_coset related

nz_coset(n): Type = {x: coset(Z,nZ(n))| x /= nZ(n)}

two_not_zero_coset: LEMMA
   FORALL(n: posnat| n > 1):
       odd?(n) => 2+nZ(n) /= nZ(n)

nz_coset_product_is_nz_coset: LEMMA
  FORALL (n: posnat, x1: [nz_coset(n), nz_coset(n)]):
    prime?(n) =>
     product[int, restrict[[numfield, numfield], [int, int], numfield](+),
             restrict[[numfield, numfield], [int, int], numfield](*)]
            (Z, nZ(n))(x1)
         /= nZ(n)


power_zp_nz_coset(p:(prime?), a: nz_coset(p), k:nat): nz_coset(p) =
                  power[nz_coset(p), product(Z,nZ(p)),+(1, nZ(p))](a, k)

% (a+nZ(p)) ^ i = a ^ i + nZ(p)
nz_coset_power_id: LEMMA
       FORALL(p:(prime?), a:int | NOT divides(p,a)): FORALL(i:nat):
              power_zp_nz_coset(p, a + nZ(p), i)  = expt(a,i) + nZ(p)

nz_prime_is_group: LEMMA
		    FORALL (n:posnat): (prime?(n)) IMPLIES
		    	   group?[nz_coset(n),product(Z, nZ(n)), (+)(1, nZ(n))](Z(n))
			   
nz_prime_is_finite_group: LEMMA
			  FORALL (n:posnat): (prime?(n)) IMPLIES
		    	   	 finite_group?[nz_coset(n),product(Z, nZ(n)), (+)(1, nZ(n))](remove[set[int]](nZ(n), Z(n)))
			   

% - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
% Avoids inst related TCC
power_eq_one_nz_coset(p:(prime?), a:nz_coset(p)): set[posnat] = {n: posnat| power_zp_nz_coset(p, a, n) = 1+nZ(p)}

% Avoids inst related TCC
period_nz_coset(p:(prime?), a:nz_coset(p)): posnat = min(power_eq_one_nz_coset(p, a))

nz_coset_period_def_equiv: LEMMA
   FORALL(p:(prime?), a:nz_coset(p)):
     period[nz_coset(p),product(Z,nZ(p)),+(1,nZ(p))]
	(remove(nZ(p),Z(p)), a) = period_nz_coset(p, a)

Mersenne_divisor_is_odd: LEMMA
   FORALL(d: int, p:(prime?)): divides(d, Mersenne_number(p)) => odd?(d)

Mersenne_prime_divisor_not_divide_2: LEMMA
   FORALL(q, p:(prime?)): divides(q, Mersenne_number(p)) => NOT divides(q, 2)

Mersenne_divisor_Fermat_prop: LEMMA
   FORALL(q, p:(prime?)): divides(q, Mersenne_number(p))
   	     		     => power_zp_nz_coset(q, 2+nZ(q), q-1) = 1+nZ(q)

Mersenne_divisor_Fermat_prop2: LEMMA
   FORALL(q, p:(prime?)): divides(q, Mersenne_number(p))
   	     		     => power_zp_nz_coset(q, 2+nZ(q), p) = 1+nZ(q)
	
primes_is_infinite: THEOREM
   NOT is_finite(fullset[(prime?)])


END inf_prime_groups
