%------------------------------------------------------------------------------
%  Proof of the Cauchy product of series
%
%  Authors:	
%		Bruno Berto de Oliveira Ribeiro
%		Universidade de Brasília - Brazil
%
%		Mauricio Ayala-Rincón
%		Universidade de Brasília - Brazil
%
% 		Thaynara Arielly de Lima
% 		Universidade Federal de Goiás - Brazil
%
%               Mariano M. Moscato
%               AMA @ NASA LaRC - USA
%
%------------------------------------------------------------------------------

Cauchy_product: THEORY

  BEGIN

  IMPORTING reals@sigma, series_lems, reals@sigma_below_sub,
            reals@bounded_reals


  %—————- Auxiliary Lemmas ————-

  sigma_reverse: LEMMA
    FORALL(a: sequence[real]): FORALL(n:nat):
    	  sigma[below[n+1]](0, n, LAMBDA(i:below[n+1]): a(i)) =
    		   sigma[below[n+1]](0, n, LAMBDA(i:below[n+1]): a(n-i))

  conv_sum(a, b: sequence[real], n:nat): real =
  	  sigma(0, n, LAMBDA(i:nat):
      	       sigma[below[i+1]](0, i, LAMBDA(j:below[i+1]): a(j)*b(i-j)))

  partial_sum_aux: LEMMA
    FORALL(a, b: sequence[real]): FORALL(n: nat):
      conv_sum(a, b, n) =
          sigma[below[n+1]](0, n, LAMBDA(i:below[n+1]): a(n-i) * series(b)(i))

  %——————— Mertens proof auxiliary Lemmas ————
 
  Cauchy_product_aux: LEMMA
     FORALL(a, b: sequence[real], n: nat):
      conv_series?(a) AND conv_series?(b) =>
         conv_sum(a, b, n) =  series(a)(n)*inf_sum(b) +
	         sigma[below[n+1]](0, n, LAMBDA(i:below[n+1]):
                            a(n-i)* (series(b)(i)-inf_sum(b)))

  Cauchy_prod_ineq_aux: LEMMA
     FORALL(x:nnreal): 0 <= x/(1+x) AND x/(1+x) < 1

  Cauchy_product_ineq1: LEMMA
     FORALL(a, b: sequence[real], epsilon:posreal):
        conv_series?(a) AND conv_series?(b) =>
	   EXISTS(N:nat): FORALL(n: nat| n >= N):
	    abs(series(a)(n)-inf_sum(a))*abs(inf_sum(b)) < epsilon

  Cauchy_product_ineq2: LEMMA
     FORALL(a, b: sequence[real], epsilon:posreal):
        conv_series?(abs(a)) AND conv_series?(b) =>
 	    EXISTS(N:nat): FORALL(n: nat| n >= N):
	     sigma[below[n+1]](N, n,
	     	LAMBDA(i:below[n+1]):abs(a(n-i))*abs(series(b)(i)-inf_sum(b)))
				< epsilon

  conv_ineq_aux_exists: LEMMA
    FORALL (b: sequence[real] | conv_series?(b)):
       nonempty?[real]
        ({x: real | EXISTS (i: nat): x = abs(series(b)(i) - inf_sum(b))})
       AND
       above_bounded[real]
         ({x: real | EXISTS (i: nat): x = abs(series(b)(i) - inf_sum(b))})

  conv_ineq_aux(b:sequence[real]| conv_series?(b)): nnreal = 
  	sup({x: real| EXISTS(i:nat): x = abs(series(b)(i)-inf_sum(b))})


  Cauchy_product_ineq3: LEMMA
     FORALL(a, b: sequence[real], epsilon:posreal, k:posnat):
        conv_series?(a) AND conv_series?(b) =>
 	    EXISTS(N:nat | N >= k): FORALL(n: nat| n >= N):
	     sigma[below[n+1]](0, k-1,
	     	LAMBDA(i:below[n+1]):abs(a(n-i))*abs(series(b)(i)-inf_sum(b)))
				< epsilon

  %———————– Cauchy product —————-
  
  Cauchy_product_convergence: LEMMA
     FORALL(a, b: sequence[real]):
      conv_series?(abs(a)) AND conv_series?(b) =>
      	  convergence(series(LAMBDA(i:nat):
	        sigma[below[i+1]](0, i, LAMBDA(j:below[i+1]): a(j)*b(i-j))),
		inf_sum(a) * inf_sum(b))

  Cauchy_product_conv: LEMMA
     FORALL(a, b: sequence[real]):
      conv_series?(abs(a)) AND conv_series?(b) =>
      	  conv_series?(LAMBDA(i:nat):
	        sigma[below[i+1]](0, i, LAMBDA(j:below[i+1]): a(j)*b(i-j)))

  Cauchy_product: LEMMA
     FORALL(a, b: sequence[real]):
      conv_series?(abs(a)) AND conv_series?(b) =>
          inf_sum(a) * inf_sum(b) =
	      inf_sum(LAMBDA(i:nat):
	        sigma[below[i+1]](0, i, LAMBDA(j:below[i+1]): a(j)*b(i-j)))

  END Cauchy_product
