%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% Authors: Ana Cristina Rocha Oliveira(*)
%          Mauricio Ayala-Rincon(*)
%          Maribel Fernandez(**)
%          (*)Universidade de Brasilia, (**)King's College London
% Last modified: 27 April 2015
% This subtheory contains specifications and formalisations
% of nominal terms.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%% Nominal Terms %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Nominal terms grammar: atoms, suspended variables, unit, pairs,
% abstractions and application, respectively.
% Definition 2.2
term[atom : TYPE+, perm : TYPE+, variable : TYPE+, symbol : TYPE+ ]: DATATYPE
	  BEGIN
	      at (a: atom): atom?

	      * (p: perm, V: variable): susp?

	      unit: unit?

	      pair (term1: term, term2: term): pair?

	      abs (abstr: atom, body: term): abs?

	      app (sym: symbol, arg: term): app?

	  END term
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
nominal_term: THEORY
 BEGIN

% variables as natural numbers
variable: TYPE = nat
  X, Y: VAR variable

% function symbols as strings
symbol: TYPE = string

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
IMPORTING atoms,
	  term[atom, perm, variable, symbol]

  pi, pi1, pi2: VAR perm
          t, s: VAR term
	  a, b: VAR atom
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Homomorphic extension of action of permutations over terms
% Definition 2.4
ext(pi)(t): RECURSIVE term =
   CASES t OF
         at(a): at(act(pi)(a)),
   	 *(pm, v): *(append(pi, pm), v),
	 unit: unit,
   	 pair(t1,t2): pair(ext(pi)(t1),ext(pi)(t2)),
   	 abs(ab, bd): abs(act(pi)(ab), ext(pi)(bd)),
   	 app(sl, ag): app(sl, ext(pi)(ag))
  ENDCASES
MEASURE t BY <<

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The null permutation is identity over terms.
ext_identity: LEMMA
   ext((::))(t) = t

% Applying an append of two permutations to terms is equal 
% to apply one by one.
ext_append: LEMMA
   ext(append(pi1,pi2))(t) = ext(pi1)(ext(pi2)(t))

% permutations do not change the structure of terms.
ext_preserves_type: LEMMA
  ord(ext(pi)(t)) = ord(t)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% Depth of terms.
% Definition 2.3
depth(t): RECURSIVE nat =
   CASES t OF
   	 at(a): 0,
	 *(pm,v): 0,
	 unit: 0,
	 pair(t1,t2): 1 + max(depth(t1), depth(t2)),
	 abs(a,bd): 1 + depth(bd),
	 app(sl,ag): 1 + depth(ag)
   ENDCASES
   MEASURE t BY <<

% Permutations do not change the depth of terms.
ext_preserves_depth: LEMMA
   depth(ext(pi)(t)) = depth(t)

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% "occurs" verify if X occurs inside the term t.
occurs(X, t): RECURSIVE bool =
   CASES t OF
   	 at(a): FALSE,
	 *(pm,v): X=v,
	 unit: FALSE,
	 pair(t1,t2): occurs(X,t1) OR occurs(X,t2),
	 abs(a,bd): occurs(X,bd),
	 app(sl,ag): occurs(X,ag)
   ENDCASES
   MEASURE t BY <<

% Permutations do not change the occurence of variables in a term.
ext_preserves_occurs: LEMMA
   occurs(X,ext(pi)(t)) = occurs(X,t)

% "Vars" computes the set of variables in a term.
% Definition 2.8
Vars(t): RECURSIVE finite_set[variable] =
   CASES t OF
   	 at(a): emptyset,
	 *(pm,v): singleton(v),
	 unit: emptyset,
	 pair(t1,t2): union(Vars(t1),Vars(t2)),
	 abs(a,bd): Vars(bd),
	 app(sl,ag): Vars(ag)
   ENDCASES
   MEASURE t BY <<

% Permutations do not change the set of variables of a term.
ext_preserves_Vars: LEMMA
   Vars(ext(pi)(t)) = Vars(t)

% The variable X is the set of variables of a term iff
% X occurs in such term.
Vars_occurs: LEMMA
   Vars(t)(X) = occurs(X,t)

END nominal_term


  






