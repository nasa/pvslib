fast_approx_props : THEORY

%----------------------------------------------------------------------
%    fast_approx_props
%    _________________
%
%     This file collects the properties about the functions in
%     the file fast_approx.pvs. It is provided to give assurance 
%     that the approximations in fast_approx are good approximations. 
%
%     -Aaron Dutle NASA Langley (Mar 2015)
%----------------------------------------------------------------------

  BEGIN
 
  IMPORTING sin_props, cos_props, tan_props, atan_props

     eps: VAR posreal
       x: VAR real
  pihalf: VAR posreal
      
  sqrt_close: LEMMA x>=0 IMPLIES
       abs(sqrt(x)-sqrt_fast_approx(x,eps))<=eps

  sin_close: LEMMA abs(x)<7 IMPLIES
       abs(sin(x)-sin_fast_approx(x,eps)) <= eps

  cos_close: LEMMA (abs(x)<7 AND eps<1) IMPLIES 
       abs(cos(x)-cos_fast_approx(x,eps)) <= eps

  tan_close: LEMMA (abs(x)<=1.3 AND eps<=10^-1) OR 
                (abs(x)<=3.139/2 AND eps<=10^-4) IMPLIES
       abs(tan(x) - tan_fast_approx(x,eps))<=eps
  
  pi_close: LEMMA  
       abs(pi - pi_fast_approx(eps))<= eps AND
       abs(pi - pi_fast_approx_br(eps))<= eps

  pi_fast_approx_pos : LEMMA
    eps <= 1 IMPLIES
    pi_fast_approx(eps) > 0 AND
    pi_fast_approx_br(eps) > 0 

  pihalf_close: LEMMA 
       abs(pi/2 - pihalf_fast_approx(eps))<= eps/2 AND 
       abs(pi/2 - pihalf_fast_approx_br(eps))<= eps/2

  pihalf_fast_approx_pos : LEMMA
    eps <= 1 IMPLIES
    pihalf_fast_approx(eps) > 0 AND
    pihalf_fast_approx_br(eps) > 0 

  atan_close_pihalf: LEMMA abs(pi/2-pihalf)<=eps/2 IMPLIES
       abs(atan(x)-atan_fast_approx_pihalf(x, eps, pihalf))<=eps

  atan_close: LEMMA eps <= 1 IMPLIES
       abs(atan(x)-atan_fast_approx(x, eps)) <= eps

  precision_le_1 : LEMMA
    FORALL (n:nat) : 0 < 10^-n <= 1
  
  END fast_approx_props
